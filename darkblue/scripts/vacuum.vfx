require scripts\tri.vfx
require scripts\bubble.vfx
require darkblue-math.vfx
require path.vfx

class: vacuum
    enemy derive
    0 firing-rate ! 
    prop my-bubble
class;

0 value bub
\ : blowout-r  bub -> r @ 100. p* p>f  ;
\ : blowout-r  150e ;
: blowout-r  200e ;

: blowout-tris
    actives>
        me tri is? -exit
        
        owner @ bub = if
            1 worth ! \ restore worth
            4. r !    \ restore normal tri radius
            floating  \ disable bubble constraint physics
        else
            bub me blowout-r 2e 0e repelling-force2
                vx 2!
        then ;

vacuum :: kill
    my-bubble @ to bub
    blowout-tris
    my-bubble @ out
    me unload ;

: constraint-physics
    owner @ ` me ` owner @ -> r @ p>f ` 0e ` bubble-constraint-force
    vx 2+!
    vx 2@ x 2+!
    owner @ -> vx 2@ x 2+! ;

: capture ( tri - )
    {: | bub :} 
    my-bubble @ to bub
    1 bounty +!
    0.25 bub -> r +! 
    [[
        0 worth !
        bub owner !
        bub -> x 2@ bub -> r @ + 6. - x 2!   \ reposition at bubble "entrance"
        ['] constraint-physics phys !
        90. rnd 225. + 1. vec vx 2!  \ randomized upward velocity 
        10. r !
    ]] ;

: suck
    act>
        pathvel x 2+! 
        actives>
            me tri is? -exit
            owner @ ?exit
            you me 150e 200e attracting-force vx 2+!
            you me 10. near? if me you [[ capture ]] then ;

: embody
    \ Position the bubble above the vacuum enemy's head
    x 2@ 2>r
    
    [[ x 2@  0 r @  2-  vx 2@ 2+ ]] ``
    0 ` r @ negate 1. - ` 2+
    x 2!
    
    \ Track motion for captured tris
    2r> x 2@ 2swap 2- vx 2! ;

: bubble-act
    act> owner @ embody physics> ;

vacuum :: init ( - )
    suck ;

public

path[ infinity-path
    segments[
        58 28 at 1.0 waypoint [[ 28.4 0.0 28.0 28.9 curve! ]]
        58 0 at 1.0 waypoint [[  25.8 -2.2 19.6 -29.8 curve! ]]
        0 28 at 1.0 waypoint [[  -25.8 0.0 -32.4 26.7 curve! ]]
        0 0 at 1.0 waypoint [[   -17.8 0.0 -26.7 -27.6 curve! ]]
    segments]
    easing[
        1.0 waypoint [[ 1 ease!   0.15 0.15 strength! ]]
        1.0 waypoint [[ 2 ease! 0.15 0.15 strength! ]]
        1.0 waypoint [[ 1 ease! 0.15 0.15 strength! ]]
        1.0 waypoint [[ 2 ease! 0.15 0.15 strength! ]]
    easing]
path]

: *vacuum
    vacuum one [[
        *bubble own [[ bubble-act me ]] my-bubble !
        infinity-path pp1 play
        0.5 pp1 -> spd !
        1.5 2. pp1 -> sx 2!
    me ]] ;

[testing]

: somewhere  gamew gameh 2rnd ;

: sprinkle  for somewhere at tri one drop loop ;

: cleanup  actives> me tri is? -exit me unload ;

just 200 150 at *vacuum as
200 sprinkle
50 150 at *player 