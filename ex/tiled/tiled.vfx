class: %tilemap
    is-a %drawable
    prop data <ref %array
    prop w
    prop h
    prop ts <ref %tileset
    32 nprop name <cstring

    :: draw ( - ) 
        ts @ ?dup -exit 0 tile pile ! 
        data @ 0 i>a w 2@ drawbg ;
class;

class: %scene
    is-a %loadable
    prop tm-main <ref %tilemap
    prop troupe <ref %array
    
    : copy-to-stage ( actor - )
        stage-slot copy stage-init ;

    :: load ( - )
        \ Instantiate actors.    
        ['] copy-to-stage troupe @ each ;
class;

require %vfxland5%/cjson/cjson.vfx
require %vfxland5%/engineer/json.vfx
also cjson>>

: _load-tiled-tilemap-layer ( cjson - )
    {: layer | tileset :}
    layer z" width" int@ w !
    layer z" height" int@ h !
    cell array{ 
        layer z" data" array-count for
            dup i int@ 1 - ,
        loop drop 
    array} data !
;

: load-tiled-tilemap-simple ( cjson - tilemap )
    \ Load tilemap from Tiled cjson object.  Assumes it's in layer 0.
    \ Also assumes the first tileset is the tileset to be used by the tilemap.
    {: ctmj | tileset :}
    %tilemap instance { 
        ctmj z" tilesets" array@ 0 object@ to tileset
        tileset if
            tileset z" source" string@ -ext f" %s.ts" >pad find if
                >body ts ! 
            else drop then
        then
        ctmj z" layers" array@ 0 object@ _load-tiled-tilemap-layer
    me } ;

: tiled-actor ( cjson - actor | 0 )
    {: obj :}
    obj z" type" ['] string@ catch if 
        2drop 
        ." No 'type' property found in JSON object, skipping..." cr
        0 exit
    then 
    f" %%%s" >pad find if 
        execute make {
            obj z" x" fixed@ 
                obj z" width" ?fixed@ 2 / + x !
            obj z" y" fixed@ 
                obj z" height" ?fixed@ 2 / - y !
        me } exit
    else
        ( $ ) f" Class %$ not found, skipping..." type cr
        0 exit
    then ;

: load-tiled-actors-simple ( ctmj - array )
    \ Load objects from Tiled cjson object.  Assumes they're in layer 1.
    {: ctmj | tileset :}
    /actor array{
        ctmj z" layers" array@ 1 object@ z" objects" array-count for
            dup i object@ tiled-actor ?dup if .summary cr then
        loop drop 
    array} ;

: scene-from-tmj ( path len <name> - ) ( - scene )
    \ Load .tmj file.  Assumes tilemap is in layer 0, tileset 0 is the 
    \ tileset to be used by the tilemap, and objects are in layer 1.
    create %scene instance {
        load-json dup 
        load-tiled-tilemap-simple tm-main !
        load-tiled-actors-simple troupe !
    } ;
