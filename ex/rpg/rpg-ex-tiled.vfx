class: %tilemap
    is-a %drawable
    prop ta :ref %array
    prop w
    prop h
    prop ts :ref %tileset
    32 nprop name :cstring

    :: draw ( - ) 
        ts @ ?dup -exit 0 tile pile ! 
        ta @ 0 nth w 2@ drawbg ;
class;

class: %scene
    is-a %loadable
    prop tm-main :ref %tilemap
    prop troupe :ref %array
    
    : copy-to-stage ( actor - )
        stage-slot copy stage-init ;

    :: load ( - )
        \ Instantiate actors.    
        ['] copy-to-stage troupe @ each ;
class;

require %vfxland5%/cjson/cjson.vfx
require %vfxland5%/engineer/json.vfx
also cjson>>

: _load-tiled-tilemap-layer ( layer-json - )
    {: layer | tileset :}
    layer "width" @int w !
    layer "height" @int h !
    cell array{ 
        layer "data" jcount for
            dup i @int 1 - ,
        loop drop 
    array} ta !
;

: load-tiled-tilemap-simple ( tmj-json - tilemap )
    \ Load tilemap from Tiled cjson object.  Assumes it's in layer 0.
    \ Also assumes the first tileset is the tileset to be used by the tilemap.
    {: tmj | tileset :}
    %tilemap make { 
        tmj "tilesets" j@ 0 j@ to tileset
        tileset if
            tileset "source" @string -ext f" %s.ts" >pad find if
                >body ts ! 
            else drop then
        then
        tmj "layers" j@ 0 j@ _load-tiled-tilemap-layer
    me } ;

: tiled-actor ( obj-json - actor | 0 )
    {: obj :}
    obj "type" ['] @string catch if 
        2drop 
        ." No 'type' property found in JSON object, skipping..." cr
        0 exit
    then 
    f" %%%s" >pad find if 
        execute make {
            obj "x" @fixed 
                obj "width" ?@fixed 2 / + x !
            obj "y" @fixed 
                obj "height" ?@fixed 2 / - y !
        me } exit
    else
        ( $ ) f" Class %$ not found, skipping..." type cr
        0 exit
    then ;

: load-tiled-actors-simple ( tmj-json - array )
    \ Load objects from Tiled cjson object.  Assumes they're in layer 1.
    {: tmj | tileset :}
    %actor object-array{
        tmj "layers" j@ 1 j@ 
        "objects" [: j@ tiled-actor ?dup if .summary cr then ;] jeach 
    array} ;

: load-scene-from-tmj ( path len <name> - ) ( - scene )
    \ Load .tmj file.  Assumes tilemap is in layer 0, tileset 0 is the 
    \ tileset to be used by the tilemap, and objects are in layer 1.
    create %scene make {
        load-json dup 
        load-tiled-tilemap-simple tm-main !
        load-tiled-actors-simple troupe !
    } ;

|| : set-meta ( bits index - ) metas-array @ swap nth dup @ rot or swap ! ;

|| : named? ( json zname - flag ) >r "name" @string r> zcount s= ; 

|| 0 value id 

|| : get-metadata ( json - )
    "properties" [: j@ "solid" named? if $0F id set-meta then ;] jeach ;

: read-tsj-properties ( path len tileset - )
    {
        #tiles @` cell` %array make metas-array !
        load-json "tiles" 
        [: 
            j@ dup "id" @int to id
            get-metadata
        ;] jeach
        me load-metas-from 
    } ;