\ ------------------------------------------------------------------------------
\ Stage - Actor System
\ ------------------------------------------------------------------------------

1024 ?constant #actors
#actors 8 * ?constant #temps
512 ?constant /actor

variable gamelife
0 0 2value gametime

class: actor
    \ is-a thinker
    is-a unloadable
    /actor field-space
    prop en
    prop x <fixed <save
    prop y <fixed <save
    prop vx <fixed <save prop vy <fixed <save 
    prop bmp <int <save 
    \ hitbox 1
    prop x1 <int <save prop y1 <int <save 
    prop w1 <int <save prop h1 <int <save 
    prop beha <xt
    prop phys <xt
    prop time <fixed                \ secs
    prop n1 prop n2 prop n3 prop n4 \ General purpose props (legacy)
    prop prio <int <save            \ 0 = behind BG, <>0 = in front of BG
    prop benched                    \ controls behavior and physics    
    prop id <int <save

    template [[ -1 id ! ]]

    static autoclass 
class;

#actors actor object-array actors 
#temps actor object-array temps

\ =============================================================================
\ Actor Access Words
\ =============================================================================

: free? ( actor - f ) -> en @ 0= ;
: actor[] ( n - actor ) actors [] ;
: actor# ( actor - n ) -> id @ ;
: act> ( - ) ( - ) 0 time !  r> beha ! ; 
: act&> ( - ) ( - ) 0 time !  r@ execute  r> beha ! ; 
: physics> ( - ) ( - ) r> phys ! ;

: exists? ( actor - f )
    dup 0= if drop 0 exit then
    dup valid-object? if drop -1 exit then
    -> en @ ;

fast[

: actives> ( - ) ( - )
    r> {: xt :}
    0 actor[] [[
        #actors for
            en @ if xt execute then
            /actor +to me
        loop
    ]] ; 

fast]

\ =============================================================================
\ Actor Inspection
\ =============================================================================

|| : .status ( - )
    benched @ if ." (BENCHED) " exit then
    en @ 0= if ." (FREE) " then ;

actor :: .summary ( actor - )
    11 push-attribute ." [ "
    me actor# dup 0 >= if ." #" . else drop me .name then
    cla @ .name
    .status
    ." at: " x 2@ 2.> swap . . 
    ." ] " pop-attribute ;

\ =============================================================================
\ Actor Errors
\ =============================================================================

: benched?  -> benched @ ;
: bench  -> benched on ; 
: ?benched  benched @ if r> drop then ;

: behave ( - )
    beha @ ?oexec ;

: !sp ( a - )
    \ Restore SP. If debug mode is on, report stack imbalances.
    debug @ if
        \ Compare SP to previous SP.
        \ The data stack grows downward.
        dup sp@ cell+ cell+ > if  
            s[ ." Positive stack imbalance in logic for actor " me .summary cr 
            s] log-warning .s
        then
        dup sp@ cell+ cell+ < if
            s[ ." Negative stack imbalance in logic for actor " me .summary cr
            s] log-warning .s
        then
    then
    sp! ;

|| : (behave) ( - )
    actives> ?benched sp@ >r behave r> !sp ;

: physics ( - )
    phys @ ?oexec ;

|| : (move) ( - )
    actives> ?benched physics ;

: +gametime ( - )
    gametime usdelta 0 d+ to gametime  1 gamelife +! ;

: +time ( - )
    ?benched pdelta time +! ;
    
|| : (+time) ( - )
    +gametime actives> +time ;

|| : (step) ( - )
    (behave) (move) (+time) ;

: step ( - )
    hold @ ?exit
    ['] (step) catch
    ?dup if
        hold on
        .throw-catch
    then ;

\ =============================================================================
\ Find actor
\ =============================================================================

|| 0 value xt

|| : (find) ( 0 actor - 0|actor )
    dup -> en @ 0= if drop exit then    
    [[ xt execute if enough drop me then ]] ;

: who> ( - actor|0 ) ( - f )
    r> to xt
    0 ['] (find) actors each ;

\ =============================================================================
\ Actor drawing
\ =============================================================================

: ?pillory ( - )
    me benched? if
        12 color s" !" print
    then ;

: sprite ( - )
    x 2@ 2.> at bmp @ cput ;

: backsprites ( - )
    batch> actives> prio @ ?exit sprite ?pillory ;

: sprites ( - )
    batch> actives> prio @ -exit sprite ?pillory ;

\ =============================================================================
\ Actor management
\ =============================================================================

variable next#

: floating ( - )
    physics> vx 2@ x 2+! ;

actor template [[
    en on
    prio on
    -5 -5 x1 2!  10 10 w1 2!
    floating
]]

: priority ( n - )
    0 #actors 1 - clamp next# ! ;

actor :: unload ( - )
    \ ." Actor unloaded " me .summary cr
    \ .rs
    en off  ; \ 0 bmp !  0 beha !  0 phys ! ;

: clear-stage ( - )
    0 next# !  actives>  me unload ;

: just ( - )
    ." JUST >> Clearing all actors!" cr
    clear-stage gamew 2 / gameh 2 / at  ;

0 actor :construct ( - )
    at@ 2>. x 2! ;

: (one) ( class - actor )
    {: c | saved-me :} 
    me to saved-me
    #actors for
        next# @ actor[] dup free? if
            c over construct
            next# @ over -> id !
            unloop exit
        then  drop
        next# @ 1 + #actors mod next# !
    loop  -1 abort" one : Out of actors." ;

: one ( class - actor )
    (one) dup init ;
    
: *actor ( - actor )
    actor one ;

actor object dummy 

\ =============================================================================
\ Object collisions
\ =============================================================================

: hitbox@ ( - x1 y1 x2 y2 )
    x 2@ 2.> x1 2@ 2+  w1 2@ 2over 2+  1 1 2- ;
    
: hit? ( obj obj - f )
    over free? over free? or if 2drop 0 exit then
    >r [[ hitbox@ ]] r> [[ hitbox@ ]] overlap? ;

\ =============================================================================
\ Scripting utilities
\ =============================================================================

: att ( col row - ) gap dup 2*  gap 2 / dup 2+  at ;

: *[[ ( class - <code> ]] )
    \ Parse-ahead instantiation for serialized objects/actors
    \ Allows property settings between *[[ ]] that can be picked up by INIT
    s" (one) [[ " evaluate
    ']' parse' evaluate  1 >in +!
    s" me init ]]" evaluate ; immediate

: passed? ( n - f ) time @ <= dup if 0 time ! then ;
: from ( actor x y - ) rot as> x 2@ 2.> 2+ at  ; \ me actor# priority ;
: halt  beha off  0 0 vx 2! ;
: morph ( class - )
    >r x 2@ vx 2@
    default me /actor move
    r> cla !
    vx 2! x 2! init ;

: vanish  y @ 16384. or y ! ;
: appear  y @ 16384. invert and y ! ;
: expanded ( a1 n - a2 )
    swap dummy copy as>
        dup negate dup x1 2+! 2 * dup w1 2+!
        me ;
: hitbox! ( x y w h - ) w1 2! x1 2! ;
: freemove ( - )
    halt floating act>
        <left> held? if -2. x +! then
        <right> held? if 2. x +! then
        <up> held? if -2. y +! then
        <down> held? if 2. y +! then ;

\ =============================================================================
\ Timer actors
\ =============================================================================

c: timer @actor xt1 dur ;

: clear-timers ( -- )
    actives> me timer is? -exit me unload ;

|| : act-timer
    act&> dur @ passed? -exit me unload xt1 @ >r ;
    
|| : (timer) ( time. xt -- )
    xt1 ! 0 max dur ! act-timer ;

: timer> ( time. - ) ( - )
    r> timer one [[ (timer) ]] ;

\ =============================================================================
\ Secondary actor storage
\ =============================================================================

|| variable tnext#
: temp ( n - a ) temps [] ;
: *temp ( - a )
    #temps for
        tnext# @ temp dup free? if
            unloop exit
        then
        drop
        tnext# @ 1 + #temps mod tnext# !
    loop  -1 abort" Out of temps" ;

: temps> ( - ) ( - )
    r> #temps for
        i swap >r temp [[ r@ execute ]] r>
    loop drop ;

: clear-temps ( - )
    temps> me unload ;

\ =============================================================================
\ Script Classes
\ =============================================================================

: (actor-definition) ( class - )
    dup body> >name count CurrSourceName -path -ext compare 0= if
        private
    then
    template to me \ allows immediate assignment of template (TODO: revisit)
;

actor :derive ( source-class derived-class - )
    nip (actor-definition) ;

: hide-autoclass ( a len - )
    \ Hide existing autoclass if any to fix LOCATE,
    \ which would otherwise take us to the call to reheat-scripts
    \ in cases where we want to (re)define it within a script.
    >pad find if
        dup execute -> autoclass @ if
            >name hidename
        else drop then 
    else drop then ;

actor :reopen ( class - )
    \ Hide generated autoclass when defining an actor class.
    \ This will cause a new class to be created. (see above for rationale)
    \ TODO: There's a better way: modify the class's source file string.  
    \       (Hopefully there's a string pointer we can change.)
    \       This will prevent any dangling references to autoclasses. 
    dup >name count hide-autoclass 
    (actor-definition) ;


20 cell stack script-classes

class: actor-script
    is-a _totem
    static script-class \ <ref _class
    
    template [[
        actor script-class !
    ]]

    extension: vfx
    default-dir: scripts

    :: _intake ( - )
        me >r   srcpath$ count required   r> to me ;
class;

actor-script script-classes push

actor-script :derive ( parent derived - )
    nip 
    dup script-classes contains? if drop exit then
    script-classes push ;

actor :: on-error ( xt err# - )
    {: xt err :} 
    err -exit
    me bench
    xt .name ." >> Actor threw an error - disabling "
    me ['] .summary catch drop cr
    .s
    err throw ;
    
: find-class ( a len - class | actor )
    >pad find if execute else drop actor then ;

|| : has-image? f" %s.png" >pad find nip ;

: >default-dir ( fn len class - path len )
    -> default-dir$ count f" %s\%s" ;

: +script (  script-totem-class <basename> - actor-script )
    \ Create an actor script containing some boilerplate.
    {: class0 | n[ 256 ] fn len :}
    bl parse f" %s.vfx" to len to fn
    fn len class0 >default-dir 2dup
    file-exists? abort" Script already exists"
    class0 materialize as>    
        my-name -ext n[ place
        srcpath$ count file-exists? not if
            srcpath$ count write[
                n[ count 
                f" class: %s" line,
                script-class @ body>name
                f"     %s derive" line,
                n[ count has-image? if
                    n[ count 
                    f"     template [[ %s.png bmp ! ]]" line,
                then
                s" class;" line,
                nl,
                n[ count 
                f" %s :: init ( - )" line,
                s"     act> ;" line,
                nl,
            write]
            srcpath$ count included
        then
        n[ count f" locate %s" evaluate 
        me ;

: +actor ( - <name> )
    actor-script +script drop ;