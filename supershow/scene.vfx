1 constant SCENE_FORMAT_VERSION

class: actor
    prop owner <ref actor
class;

class: scene
    is-a _totem
    :: default-path ( - path len ) s" dat/scn" ;
class;

: deser-actor ( a - a' )
    ['] one desbin ;

|| : serialize?  [[ en @   owner @ 0= and ]] ;

scene :: _commit ( - )
    \ save actors on stage to disk
    temp[ 
        SCENE_FORMAT_VERSION ,
        me serbin
        actors ['] serialize? ['] serbin sermulti
    temp] srcpath count ['] write catch abort" File write error" ;

: load-actor ( class - actor )
    dup actor is? not if
        body> >name count
        f" Class %s does not inherit from ACTOR." -1 -rot .abort 
    then 
    one ;

: load-scene-data ( a - )
    @+ dup SCENE_FORMAT_VERSION <> if
        -1 swap f" Unsupported scene format version %n" .abort
    else drop then 
    ['] into-me desbin
    ['] load-actor ['] desbin desmulti 
    drop ;

scene :: _load ( - )
    \ load actors from disk to stage
    srcpath count file-exists? -exit
    srcpath count file@ drop
    dup load-scene-data
    free throw ;

: +scene ( <name> - scene ) 
    scene +totem ;