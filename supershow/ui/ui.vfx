\ ------------------------------------------------------------------------------
\ User Interface Layer
\ ------------------------------------------------------------------------------

public

256 ?constant #elements
#elements /actor array elements

\ Object extension
var sx var sy
var en var vis var draw
var parent var lowest var highest var next var prev
require %idir%\..\dltree.vfx

\ Allocation
: element ( n - a )  /actor * elements + ;
|| variable enext#

borrow stage~ unsafe-free?

|| : alloc-element ( - a )
    #elements for
        enext# @ element dup unsafe-free? if
            object% >object
            unloop exit
        then  drop
        1 enext# +!
    loop  -1 abort" Out of elements" ;
|| : 1element  cla !  at@ 2s>p x 2!  vis on  1. 1. sx 2!  en on ;
: el ( class - actor )
    me >r alloc-element [[
        me r> dlpush
        1element me init
        me
    ]] ;

\ Definition
: el: ( class - <name> a )
    el dup constant ;
: draw> ( - ) ( - )
    r> draw ! ; DoNotSin

\ Announce
|| 0 value xt
|| : (announce)
    xt execute [ last-colon-xt @ ] literal me dleach ;
: announce ( xt el - )
    swap to xt ['] (announce) swap dleach ;
: announce> ( el - <code> ; )
    r> swap announce ;

\ Update element time counters
|| : (+time) ( - )
    en @ -exit  pdelta time +! ;
|| : time-children ( element - )
    ['] (+time) swap dleach ;

\ Logic
|| : (act) ( - )
    en @ -exit
    beha @ oexec
    [ last-colon-xt @ ] literal me dleach ;
|| : act-children ( element - )
    ['] (act) swap dleach ;

\ Rendering
|| : middle ( - x y )  bmp @ bmpwh 2 2 2/ ;
|| : sprite ( - )  at@ middle 2negate +at bmp @ put at ;
|| : +t  x 2@ 2p>s 2s>f `` sx 2@ 2p>f 0e ``` transform ;
|| : (draw) ( - )
    en @ -exit vis @ -exit
    +m +t 0 0 at
        draw @ if draw @ oexec else sprite then
        [ last-colon-xt @ ] literal me dleach
    -m  ;
|| : draw-children ( element - )
    ['] (draw) swap dleach ;

\ Animation
\ -- : (cycle-recursive) 
\   +animation [ last-colon-xt @ ] literal me dleach ;
\ -- : cycle-children ( - )
\   ['] (cycle-recursive) swap dleach ; is cycle-children

\ Processing
0 0 at alloc-element dup constant root [[ object% 1element ]]
: ui ( - )
    \ 0 0 winw winh al_set_clipping_rectangle
    root act-children
    root draw-children
    root time-children
\    root cycle-children
    ; \ fullscreen? if clip-screen then ;

\ Coordinates
: p>local ( x. y. - x. y. )
    parent @ ?dup if [[ recurse ]] then x 2@ 2- sx 2@ 2p/ ;
: >local ( x y - x y )
    2s>p p>local 2p>s ;

\ Update
|| 0 value c

: ui-update ( - <class> )
    \ Live update element class
    \ TODO: make `update` polymorphic since classes are now objects
    bl preparse f" scripts\%s.vfx" >try
    bl word count f" %s%%" >pad find not if drop exit then
    >body to c
    root announce> me c is? -exit me init ;

\ Management
|| : (gone) en off ;
: isolate ( el - )
    ['] (gone) over 's parent @ dleach  's en on ;

\ Element script classes

: element-oexec-error-handler ( n - )
    ?dup -exit
    cr ." oexec >> Error on element, disabling: " me peek
    me bench
    throw ;

: element-class ( - <name> )
    script-class
    ['] element-oexec-error-handler swap 's oexec-error-handler ! ;
    
\ ------------------------------------------------------------------------------

\ class test
\ test :: init draw> s" Helo worl" print ;
\ root [[
\     0 0 at test el value e1
\     50 50 at test el value e2
\ ]]
