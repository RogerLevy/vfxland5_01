65536 ?constant #bitmaps

|| #bitmaps cell array bitmaps

|| : bitmap[]  $FFFF and bitmaps [] ;

variable next-bmp#  1 next-bmp# !

: alloc-bmp# ( - bmpid ) next-bmp# @   1 next-bmp# +! ;
: add-bitmap ( bitmap - bmpid ) alloc-bmp# dup >r bitmap[] ! r> ;

class: %bitmap
    %asset derive
    prop id 
    prop albmp

    : does-bitmap ( - ) 
        does> -> id @ ;

    : _load-albmp ( - )
        me data@ memfile@ dup
            0 al_load_bitmap_f albmp !
        -memfile ;

    redef : bitmap ( <path> - )
        bl parse 
        %bitmap asset does-bitmap { 
            _load-albmp 
            me add-bitmap id !
        } ;

    :: on-startup ( - )
        _load-albmp ;

class;

: id>albmp ( bmpid - albmp ) 
    bitmap[] @ -> albmp @ ;

: id>bmp ( bmpid - bitmap ) 
    bitmap[] @ ;

: ?nullbmp  dup 0= abort" BITMAP-FILE : Bitmap is null!" ;
: ?subbmp  dup al_is_sub_bitmap if al_get_parent_bitmap then ;

: bitmap-file ( bmpid - a n ) 
    ?nullbmp ?subbmp id>bmp -> srcpath$ count ;

: bmpwh ( bmpid - w h )
    id>albmp ?dup if dup albmpw swap albmph else 0 0 then ;

: bmpw ( bmpid - w )
    bmpwh drop ;

: bmph ( bmpid - h )
    bmpwh nip ;

: put ( bmpid - ) 
    \ draw a sprite; takes a bitfield $F000IIII
    dup id>albmp swap 24 rshift draw-bitmap ;

: cput ( bmpid - ) 
    \ draw sprite with center as the origin
    dup dup bmpwh -2 -2 2/ +at put bmpwh 2 2 2/ +at ;
