\ ==============================================================================
\ SKETCHPAD mode

\ SKETCHPAD/ vocabulary
\ THIS - value pointing to the current scene
\ SAVEAS <name> - create a scene (along with the file) and assign it to THIS.
\                 no extension.
\ EDIT <name>   - create or load a scene - no extension.

\ ( startup: create scenes from every file in dat/scn/ and scenes/ )

\ ( make a bunch of actors )

\ saveas myscene   \ create a scene

\ ( make some changes )

\ CTRL+S - saves THIS

\ otherscene load   \ instantiate more actors from pre-existing scene

\ edit otherscene   \ set THIS to the given scene. this would clear the stage 
                    \ and load the scene.
\ ==============================================================================

include %vfxland5%/lib/mouse.vfx

\ ------------------------------------------------------------------------------

private

0 value this

\ Commands

: save ( - )
    this 0= 
        abort" No current scene!  Required: `SAVEAS <scene>` or `EDIT <scene>`"
    this keep ;    

: (friendly-scene)  bl parse f" +scene %s.scn" evaluate ;

: saveas ( <name> - )
    (friendly-scene) to this   save ; 

: edit ( <name> - )
    (friendly-scene) to this   just this load ;

: ctrl+s  save ;

: order-swap  get-order >r swap r> set-order ;

: as  as  ['] sketchpad/ >body @ +order  order-swap ;

\ =============================================================================

0 value mx 
0 value my
0 value dragee
\ create anchor 0 , 0 ,

: editable? ( obj - flag ) 
    serializable? ;

: bmpbox
    {: | w h :}
    bmp @ bmpwh to h to w
    x 2@ 2.> w h 2 2 2/ 2- 2dup w h 2+ ;

: pick ( - obj | 0 )
    0
    actives> 
        bmp @ -exit
        me editable? -exit
        mouse 2dup bmpbox overlap? if
            \ bmp @ bitmap-file type cr
            drop me 
        then ;

: copy-actor ( obj - obj` )
    actor one copy
    dup actors index over -> id ! ;

: ?copy ( obj - obj` )
    ctrl? if copy-actor ." copy" cr then  ;

: dragging ( - )
    lb <lb - 
    case
        1 of pick ?dup if dup as ?copy dup .summary to dragee then endof
        0 of dragee ?dup if [[ lb if mickey 2>. x 2+! then ]] then endof
        -1 of 0 to dragee endof
    endcase ;

: deleting ( - )
    rb <rb - 
    case
        1 of pick ?dup if unload then endof
    endcase ;

: input ( - )
    get-mouse
    dragging 
    deleting 
;

variable flicker-ctr

: flicker ( - )
    1 flicker-ctr +!  flicker-ctr @ 1 and ?exit sprite ;

: sprites ( - )
    batch> actives> me editable? if sprite else flicker  then ;

borrow forth hold  \ import system variable
aka _hold hold     \ and shadow it
variable hold      \ new private variable will work with f8 and persist

\ ------------------------------------------------------------------------------

public

: sketchpad
    hold on
    repl on
    sketchpad/ 
    show> hold @ _hold ! step sprites input ;

honing @ [if]
sketchpad
push-order
[then]