\ ==============================================================================
\ SKETCHPAD mode

\ SKETCHPAD: vocabulary
\ THIS - value pointing to the current scene
\ SAVEAS <name> - create a scene (along with the file) and assign it to THIS.
\                 no extension.
\ EDIT <name>   - create or load a scene - no extension.

\ ( startup: create scenes from every file in dat/scn/ and scenes: )

\ ( make a bunch of actors )

\ saveas myscene   \ create a scene

\ ( make some changes )

\ CTRL+S - saves THIS

\ otherscene load   \ instantiate more actors from pre-existing scene

\ edit otherscene   \ set THIS to the given scene. this would clear the stage 
                    \ and load the scene.
\ ==============================================================================

private

fload %vfxland5%/lib/mouse.vfx
fload %vfxland5%/lib/help.vfx

\ ==============================================================================

public

defer sketchpad-overlay
['] noop is sketchpad-overlay

private

0 value this

\ ==============================================================================

\ Commands

command : save ( - )
    \ Save current scene.
    this 0= 
        abort" No current scene!  Required: `SAVEAS <scene>` or `EDIT <scene>`"
    this keep ;    

: (friendly-scene)  bl parse f" +scene %s.scn" evaluate ;

command : saveas ( <name> - )
    \ Save stage as scene with given name.
    (friendly-scene) to this   save ; 

command : edit ( <name> - )
    \ Edit scene of given name.
    (friendly-scene) to this   just this load ;

command : ctrl+s ( - )
    \ Hotkey: Save current scene.
    save ;

: order-swap  get-order >r swap r> set-order ;

command : as ( obj - )
    \ Scope into the given object.
    as  ['] sketchpad: >body @ +order  order-swap ;

\ =============================================================================

0 value mx 
0 value my
0 value dragee

: editable? ( obj - flag ) 
    serializable? ;

: bmpbox
    {: | w h :}
    bmp @ bmpwh to h to w
    x 2@ 2>i w h 2 2 2/ 2- 2dup w h 2+ ;

: pick ( - obj | 0 )
    0
    actives> 
        bmp @ -exit
        me editable? -exit
        mouse 2dup bmpbox overlap? if
            drop me 
        then ;

: copy-actor ( obj - obj` )
    %actor one copy
    dup actors index over -> id ! ;

: ?copy ( obj - obj` )
    ctrl? if copy-actor ." copy" cr then  ;

: dragging ( - )
    lb <lb - 
    case
        1 of pick ?dup if dup as ?copy dup .summary to dragee then endof
        0 of dragee ?dup if { lb if mickey 2>p x 2+! then } then endof
        -1 of 0 to dragee endof
    endcase ;

: deleting ( - )
    rb <rb - 
    case
        1 of pick ?dup if unload then endof
    endcase ;

: input ( - )
    get-mouse
    dragging 
    deleting 
;

variable flicker-ctr

: flicker ( - )
    1 flicker-ctr +!  flicker-ctr @ 1 and ?exit me draw ;

: sprites ( - )
    batch> actives> me editable? if me draw else flicker then ;

aka _hold hold     \ shadow system variable
variable hold      \ new private variable will work with f8 and persist

variable 'oldshow
: ?toggle-mode
    'show @ ip>nfa name> r@ ip>nfa name> = if
        r> drop \ this is fine with locals because it's not a continuation
        'oldshow @ ip>nfa name> execute
    else
        'show @ 'oldshow !
    then ;

\ =============================================================================

private

create bgc 0.5e sf, 0.5e sf, 0.5e sf, 1e sf,

redef : cls ( - )
    bgc 4f@ al_clear_to_color ;

command : backdrop ( f-r f-g f-b f-a - )
    \ Set tool backdrop color.
    bgc 4f! ;

public

: sketchpad
    ?toggle-mode
    hold on
    repl on
    sketchpad: 
    gamew 2 / gameh 2 / at 
    show> 
        hold @ _hold ! 
        cls
        step sprites sketchpad-overlay
        input ;

\ =============================================================================

honing @ [if]
sketchpad
push-order \ TODO: is this needed anymore? (see ?HONE)
[then]