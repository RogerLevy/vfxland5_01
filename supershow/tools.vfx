\ =============================================================================
\ Interactive tools
\ =============================================================================

also engineer~

|| 0 value c

|| : (resurrect) ( - )
    benched off me init ;

: init-by-class ( class - )
    to c
    actives> me c is? if (resurrect) then ;
    
cstring update-name$

|| : hone-class ( - )
    update-name$ count f" update %s" evaluate ;
    
: update ( - [<class>] )
    \ Live update actor class
    \ Activates honing mode with custom command (see HONE-CLASS)
    \ If not class is given, updates the current object's class
    {: | class0 :}
    bl parse
    dup if
        >pad find not abort" Class not found!"
        >body to class0
        pad count update-name$ place
    else
        2drop
        me valid-object? not abort" ME is not a valid object - nothing to update!"
        cla @ to class0
        class0 body> >name count update-name$ place
    then
    
    ['] hone-class honing !
    update-name$ count f" %s.vfx" file-exists? if
        update-name$ count f" %s.vfx" >try
    else
        update-name$ count f" scripts\%s.vfx" >try
    then
    class0 init-by-class
    update-name$ count f" %s~" $private ;

aka hone update

: .actors ( - )
    actives> me describe cr ;

: .temps ( - )
    temps> me free? ?exit me describe cr ;

|| : has-image? f" %s.png" >pad find nip ;

: script ( - <name> )
    \ Create an actor script containing some boilerplate.
    {: | n[ 256 ] #1 :}
    bl parse n[ place
    n[ count f" scripts\%s.vfx" file-exists? not if
        n[ count f" scripts\%s.vfx" file[
            n[ count f" actor-class %s" line,
            nl,
            n[ count f" %s :: init ( - )" line,
            n[ count has-image? if
                n[ count f"     %s.png bmp !" line,
            then
            s"     act> ;" line,
        file]
        n[ count f" scripts\%s.vfx" included
    else
        -1 abort" Script already exists"
    then
    n[ count f" locate %s" evaluate ;
    
: edit ( - <classname> )
    \ Edit a class (minus the %).
    \ If no name given, edit the class inferred from the current object (ME).
    \ Switch to the class's vocabulary.
    0 preparse nip 0= if
        cla @ body> >name count
    else
        bl parse 
    then
    2dup f" %s~" evaluate
    f" locate %s" evaluate ;

|| : s= compare 0= ;

: [testing] ( - )
    \ Conditionally compile test code - active only in honing mode
    \ And only with actor classes
    honing @ ['] hone-class =
    update-name$ count 
    currsourcename -path -ext
    s= and not
    if postpone \\ exit then
    private ;

engineer~
: fp ( - )
    \ Frame profile
    ['] screen time? ;
