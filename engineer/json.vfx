\ ==============================================================================
\ Serialization Support (JSON)
\ Supports serialization of NIBS objects 
\ ==============================================================================

\ ==============================================================================
\ NIBS serialization
\ ==============================================================================

private

also cjson/

0 value val-addr

: ser-json-cell    ( zname cjson - ) val-addr @ -rot add-int ;
: ser-json-int     ( zname cjson - ) val-addr @ -rot add-int ;
: ser-json-fixed   ( zname cjson - ) val-addr @ -rot add-fixed ;
: ser-json-float   ( zname cjson - ) val-addr sf@ add-float ;
: ser-json-cstring ( zname cjson - ) val-addr count >zpad -rot add-string ;

create ser-json-table
    ' ser-json-cell ,
    ' ser-json-int ,
    ' ser-json-fixed ,
    ' ser-json-float ,
    ' ser-json-cstring ,

: ser-property-json  ( cjson property - )
    dup >zname -rot
    cla @ >property-type @ cells ser-json-table + @ execute ;

: des-json-cell    ( zname cjson - ) get-int val-addr ! ;
: des-json-int     ( zname cjson - ) get-int val-addr ! ;
: des-json-fixed   ( zname cjson - ) get-fixed val-addr ! ;
: des-json-float   ( zname cjson - ) get-float val-addr sf! ;
: des-json-cstring ( zname cjson - ) get-string val-addr place ;

create des-json-table
    ' des-json-cell ,
    ' des-json-int ,
    ' des-json-fixed ,
    ' des-json-float ,
    ' des-json-cstring ,

: des-property-json  ( cjson property - )
    dup >zname -rot
    cla @ >property-type @ cells des-json-table + @ execute ;

public

also cjson/

borrow nib2/ prop+

: add-properties-to ( obj cjson - )
    {: obj cjson | property0 tmpl-addr :}
    obj {
        cla @ >zname z" class" cjson add-string

        cla @ -> property-list list-count for
            list-next to property0
            property0 cla @ >serialize-flag @  if

                \ get the property address in the instance and in the template
                me property0 prop+ to val-addr
                cla @ >template property0 prop+ to tmpl-addr

                cjson property0 ser-property-json 
            then
        loop drop 
    } ;

: save-object-json ( object path len - )
    serjson> add-properties-to ;

: object>cjson ( object - cjson )
    create-cjson dup >r add-properties-to r> ;

: get-properties ( cjson obj - )
    {: cjson obj | property0 :}
    obj {
        cla @ -> property-list list-count for
            list-next to property0
            property0 cla @ >serialize-flag @  if
                property0 >zname cjson get-item if
                    me property0 prop+ to val-addr
                    cjson property0 des-property-json
                then
            then
        loop drop 
    } ;

: read-json ( path len - cjson )
    load-file udup parse-json swap free drop ;

