\ ------------------------------------------------------------------------------
\ Engineer 1.0 internal version 2
\ ------------------------------------------------------------------------------

\ =============================================================================
\ Core System Components
\ =============================================================================

include %idir%/vfxcore2.vfx \ Codebase-wide language extensions
include %idir%/error2.vfx   \ TODO: Make platform-agnostic
include %idir%/variables.vfx
include %idir%/counter.vfx  \ Required by mersenne for seeding
include %idir%/mersenne.vfx \ RNG
include %idir%/changes.vfx  \ Filesystem change tracking
include %idir%/totem.vfx    \ Persistence system

\ =============================================================================
\ Game services
\ =============================================================================

fload %idir%/graphics.vfx   \ Graphics
fload %idir%/input.vfx      \ Input
fload %idir%/tweak.vfx      \ Persistent variable control
fload %idir%/border.vfx     \ TV border / scaling
fload %idir%/ide3.vfx       \ Interactive IDE
fload %idir%/go.vfx         \ Main Loop

\ =============================================================================
\ Reset System Classifiers
\ =============================================================================

|| : fresh,  dup , /classifier move, ;

|| create fresh-classifiers 
    /classifier cell+ array[
        %object fresh,    
        %iterable fresh,
        %lifo fresh,
        %fifo fresh,
        %totem fresh,

        %class fresh,
        %trait fresh,
        %property fresh,
        %static fresh,
        %protocol fresh,
        %array fresh,
        %stack fresh,
        %object-array fresh,
    array]

|| : reset-classifier ( a - )
    @+ ( src dest ) /classifier move ;

|| create fresh-all-classifiers classifiers #max-classifiers cells move,

|| : reset-system-classifiers ( - )
    ['] reset-classifier fresh-classifiers each 
    fresh-all-classifiers classifiers #max-classifiers cells move ;

\ =============================================================================
\ Entry Points  
\ =============================================================================

|| : /console
    -1 line# !
    hWndMain to vfx-hwnd
    vfx-hwnd z" Engineer Dev Console" SetWindowText drop 
    consoledev op-handle ! ;

|| : vfxforth-entrypoint ( hinst hpinst lpcommand nshow -- res )
    [ ' EntryPoint >body @ compile, ] ;

borrow nib2/ init-nib

|| : empty-engineer ( - )
    reset-format-buffers
    reset-strout-buffers
    init-nib
    reset-system-classifiers
    clear-funcs
    ['] noop 'show !
    ['] noop is boot
    ['] noop is onSwitchIn
    honing off ;

redef : empty ( - )
    debug @ -exit
    empty-engineer
    only forth definitions
    s" anew gild" evaluate ;

: cartridge ( - )
    \ Load user program
    empty page hold off
    s" main.vfx" fileExist? if s" main.vfx" floaded then ;

also substitutions 
aka vfxland5 vfxland5
previous

: init-dev ( - )
    \ Development-only initialization
    empty 
    +xrefs
    256 1024 * xref-kb
    s" %bin%\.." expand vfxland5 place
    wd@ mondir
;

|| : @homepath ( - )
    wd@ '\' ending s" bin" compare 0= if
        s" .." $cwd
    then
    wd@ home-path$ place ;

: frigid ( - )
    \ Initialize runtime environment
    rs
    hold off
    debug @ if init-dev then
    @homepath
    init-mersenne
    init-allegro
    *display
    init-vga-font
    0 0 to ustime 
    init-history ;

redef : cold ( - )
    \ Release cold start sequence - initialize all systems and begin execution
    frigid boot go ;

: debug-cold ( - )
    vfxforth-entrypoint /console ;

\ =============================================================================
\ Turnkey creation
\ =============================================================================

: save-release ( - <path> )
    ['] cold is EntryPoint
    fullscreen?
        debug @ hold @
            ide off
            repl off
            debug off
            hold off
            fullscreen
            esc-quits off
            0 to GenINI?
            0 to InitSupport?
            save-exe cr
        hold ! debug !
    if fullscreen else windowed then 
    repl on
    esc-quits on ;

: save-debug ( - <path> )
    ['] debug-cold is EntryPoint
    fullscreen?
        ide @ repl @ debug @ hold @
            ide off
            repl off
            debug off \ This disables Engineer-only initialization.
                      \ Counter-intuitive because we want to debug the game not Engineer.
            hold off
            windowed
            ['] (.cold) is .cold
            -1 to GenINI?
            0 to InitSupport?
            save-exe cr
        hold ! debug ! repl ! ide !
    if fullscreen else windowed then ;

: save-dev ( - <path> )
    fullscreen?
        ide @ repl @ hold @
            ide on
            repl on
            hold off
            windowed
            save-exe cr
        hold ! repl ! ide !
    if fullscreen else windowed then ;

|| : prj ( - a len ) prj-path$ count -path ;

: turnkey ( - )
    wd@ '\' ending f" %vfxland5%\engineer\turnkey.bat %s" shellcmd ;

\ =============================================================================
\ Development tool commands
\ =============================================================================

fload %idir%/tools.vfx
include %idir%/ls.vfx

\ =============================================================================
\ Kickoff
\ =============================================================================

: engineer-entrypoint ( hinst hpinst lpcommand nshow -- res )
    over zcount zcommand zplace
    vfxforth-entrypoint ;

: engineer.cold ( - )
    \ Main Engineer entry point - initialize and run with command line args
    /console
    (.cold)
    ."  ~~~ Engineer v1 ~~~ " cr
    frigid 
    \ s" xref.xrf" file-exists? if load-xref then
    init-ide
    zcommand zcount strip-exe ide-interpret
    ?dup if .throw >vfx else go then ;

' engineer-entrypoint is EntryPoint
' engineer.cold is .cold
borrow nib2/ gild-nib
gild-nib
marker gild
