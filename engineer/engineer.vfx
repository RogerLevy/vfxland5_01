\ ------------------------------------------------------------------------------
\ Engineer 1.0 internal version 2 (regressed)
\ ------------------------------------------------------------------------------

\ =============================================================================
\ %vfxland5% string macro
\ =============================================================================

also substitutions definitions
    create vfxland5 256 allot&erase
    s" %idir%\.." expand vfxland5 place
only forth definitions

\ =============================================================================
\ Core System Components
\ =============================================================================

include %idir%/checks.vfx
include %idir%/variables.vfx \ System variables
\ include %idir%/changes.vfx  \ Filesystem change tracking

\ =============================================================================
\ Game services
\ =============================================================================

fload %idir%/graphics.vfx   \ Graphics functions
fload %idir%/input.vfx      \ Input functions
fload %idir%/border.vfx     \ TV border / scaling
\ fload %idir%/ide3.vfx       \ Interactive IDE
fload %idir%/go.vfx         \ Main Loop

\ =============================================================================
\ Reset System Classifiers
\ =============================================================================

|| : fresh,  dup , /classifier move, ;

|| create fresh-classifiers 
    /classifier cell+ array{
        %object fresh,    
        %iterable fresh,
        %lifo fresh,
        %fifo fresh,

        %class fresh,
        %trait fresh,
        %property fresh,
        %static fresh,
        %protocol fresh,
        %array fresh,
        %stack fresh,   
        %object-array fresh,
    array}

|| : reset-classifier ( a - )
    @+ ( src dest ) /classifier move ;

|| create fresh-all-classifiers classifiers #max-classifiers cells move,

|| : reset-system-classifiers ( - )
    ['] reset-classifier fresh-classifiers eacha 
    fresh-all-classifiers classifiers #max-classifiers cells move ;

\ =============================================================================
\ Entry Points  
\ =============================================================================

|| : init-vfx-console
    -1 line# !
    hWndMain to vfx-hwnd
    vfx-hwnd z" VFX Console" SetWindowText drop 
    consoledev op-handle ! ;

|| : vfxforth-entrypoint ( hinst hpinst lpcommand nshow -- res )
    [ ' EntryPoint >body @ compile, ] ;

borrow nib2>> init-nib

|| : empty-engineer ( - )
    reset-format-buffers
    reset-strout-buffers
    init-nib
    reset-system-classifiers
    clear-funcs
    ['] noop 'show !
    ['] noop is boot
    ['] noop is onSwitchIn
    honing off ;

redef : empty ( - )
    debug @ -exit
    rs
    empty-engineer
    only forth definitions
    s" anew gild" evaluate ;

: cartridge ( - )
    \ Load user program
    empty page hold off
    s" main.vfx" fileExist? if s" main.vfx" floaded then ;

also substitutions 
    aka vfxland5 vfxland5
previous

: init-dev ( - )
    \ Development-only initialization
    empty 
    +xrefs
    256 1024 * xref-kb
    s" %bin%\.." expand vfxland5 place
\    wd@ mondir
;

|| : find-home-path ( - )
    wd@ '\' ending s" bin" compare 0= if
        s" .." $cwd
    then
    wd@ home-path$ place ;

: frigid ( - )
    \ Initialize runtime environment
    rs
    hold off
    debug @ if init-dev then
    find-home-path
    init-mersenne
    init-allegro
    *display
    init-vga-font
    0d to ustime 
\    init-history 
;

redef : main ( - )
    \ Release cold start sequence - initialize all systems and begin execution
    frigid boot go ;

: debug-cold ( - )
    vfxforth-entrypoint init-vfx-console ;

\ =============================================================================
\ Turnkey creation
\ =============================================================================

: save-release ( - <path> )
    ['] main is EntryPoint
    fullscreen?
        debug @ hold @
            ide off
            repl off
            debug off
            hold off
            fullscreen
            esc-quits off
            0 to GenINI?
            0 to InitSupport?
            save-exe cr
        hold ! debug !
    if fullscreen else windowed then 
    repl on
    esc-quits on ;

: save-debug ( - <path> )
    ['] debug-cold is EntryPoint
    fullscreen?
        ide @ repl @ debug @ hold @
            ide off
            repl off
            debug off \ This disables Engineer-only initialization.
                      \ Counter-intuitive because we want to debug the game not Engineer.
            hold off
            windowed
            ['] (.cold) is .cold
            -1 to GenINI?
            0 to InitSupport?
            save-exe cr
        hold ! debug ! repl ! ide !
    if fullscreen else windowed then ;

: save-dev ( - <path> )
    fullscreen?
        ide @ repl @ hold @
            \ ide on
            \ repl on
            hold off
            windowed
            save-exe cr
        hold ! repl ! ide !
    if fullscreen else windowed then ;

|| : prj ( - a len ) prj-path$ count -path ;

: turnkey ( - )
    wd@ '\' ending f" %vfxland5%\engineer\turnkey.bat %s" shellcmd ;

\ =============================================================================
\ Development tool commands
\ =============================================================================

fload %idir%/tools.vfx
fload %idir%/ls.vfx

\ =============================================================================
\ Kickoff
\ =============================================================================
public

: engineer-entrypoint ( hinst hpinst lpcommand nshow -- res )
    over zcount zcommand zplace
    vfxforth-entrypoint ;

: engineer.cold ( - )
    \ Main Engineer entry point - initialize and run with command line args
    init-vfx-console
    (.cold)
    ."  ~~~ Engineer v2 (regress) ~~~ " cr
    frigid 
\    init-ide
    \ zcommand zcount strip-exe interpret-string \ ide-interpret
    \ ?dup if .throw else ( go ) then 
    >vfx ;

' engineer-entrypoint is EntryPoint
' engineer.cold is .cold

borrow nib2>> gild-nib
gild-nib

include %idir%/back.vfx           \ Backward compatibility shim
marker gild
