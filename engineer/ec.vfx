\ Execution context
\ Note: We can't use the USER variable system (long story)

optimising on
[defined] fast{ [if] fast{ [then]

[defined] pagealign not [if]
: pagealign ( - )  here $ff and 256 swap - allot ;
: aligned-page ( - <name> )  pagealign here $100 allot&erase constant ;
[then]

aligned-page ecs
variable ecsp ecs ecsp !
0 value s/ec

: cp@ ( - ec ) ecsp @ @ ;
: cp! ( ec - ) ecsp @ ! ;
: c{  ( ec - ) 4 ecsp c+!  cp! ;
: c}  ( - )    -4 ecsp c+! ;

: sentinels,  256 cell/ 0 do $DEADBEEF , loop ;

: econtext ( <name> - )
    create sentinels, ;

econtext root-ec  root-ec cp!

variable _ecsp
0 value ofs
0 value ec
: >ecfa ( ofs - val )
    \ find EC field in EC stack.  
    \ search for a non-$DEADBEEF, starting at top EC, returning the field if found.   
    \ stop at root-ec.
    \ if no used field found, return the top EC's field. 
    \ if root-ec not found, return the top EC's field.
    to ofs
    ecsp @ _ecsp !
    16 for
        _ecsp @ @ to ec
        ec ofs + dup @ $DEADBEEF <> if unloop exit else drop then
        ec root-ec = if cp@ ofs + unloop exit then
        -4 _ecsp c+!
    loop 
    cp@ ofs + ;

: does-ec-getter  does> @ >ecfa @ ;
: does-ec-setter  does> @ cp@ + ! ;

: ec-cell ( <name> - ) ( - n ) ( n - )
    \ Creates getter ( MYVAR@ ) and setter ( MYVAR! )
    bl parse 2dup
    f" %s@" screate s/ec , does-ec-getter 
    f" %s!" screate s/ec , does-ec-setter 
    cell +to s/ec ;

: does-ec-fgetter  does> @ >ecfa sf@ ;
: does-ec-fsetter  does> @ cp@ + sf! ;

: ec-float ( <name> - ) ( - n ) ( n - )
    \ Creates getter ( MYVAR@ ) and setter ( MYVAR! )
    bl parse 2dup
    f" %s@" screate s/ec , does-ec-fgetter 
    f" %s!" screate s/ec , does-ec-fsetter 
    cell +to s/ec ;


\ Temporary contexts
:noname  4096 256 / for sentinels, loop ; 
    pagealign here swap execute constant tcs

variable tcsp  tcs tcsp !

: t{ 
    tcsp @ dup c{
    tcs - 256 + 4095 and tcs + tcsp ! ;

: t} 
    c}
    tcsp @ tcs - 256 - 4095 and tcs + tcsp ! ;

((
\ Tests
ec-cell fnt
ec-cell fore
ec-cell pile

\ Set default values
123 fnt!
456 fore!
789 pile!

econtext test-ec
test-ec c{  -1 fore!  c}   \ pre-set test-ec's FORE to -1, leave others $DEADBEEF

: inner
    ." Inner: fnt=" fnt@ . ." fore=" fore@ . ." pile=" pile@ . cr ;

: middle
    ." Middle before: fnt=" fnt@ . ." fore=" fore@ . ." pile=" pile@ . cr
    999 pile!  \ change pile in current context
    inner
    ." Middle after: fnt=" fnt@ . ." fore=" fore@ . ." pile=" pile@ . cr ;

: outer
    ." Outer before: fnt=" fnt@ . ." fore=" fore@ . ." pile=" pile@ . cr
    test-ec c{
        middle
    c}
    ." Outer after: fnt=" fnt@ . ." fore=" fore@ . ." pile=" pile@ . cr ;

outer

: profile  
    100000 for
    pile@ drop
    test-ec c{
        pile@ drop
        999 pile!  
        pile@ drop
    c} 
    loop ;

\ Expected output:
\ Outer before: 123 456 789
\ Middle before: 123 -1 789  (FORE=-1 from test-ec, FNT/PILE inherited from default)
\ Inner: 123 -1 999  (PILE=999 set by middle)
\ Middle after: 123 -1 999
\ Outer after: 123 456 789  (back to default context values) 

))

[defined] fast} [if] fast} [then]
optimising off