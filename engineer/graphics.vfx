\ =============================================================================
\ Framerate independence
\ =============================================================================

redef : ticks  ( n - ms )
    s>f 1000e tps f/ f* f>s ;

: update-delta  ( - )
    ustime  ucounter 2dup to ustime  2swap 2-
        d>s to usdelta
    usdelta s>f 1000000e f/ to sdelta
    ustime 1000 m/ to mstime
    sdelta f>p to pdelta ;

\ =============================================================================
\ Window management
\ =============================================================================

extern void * GetForegroundWindow( );
extern bool SetForegroundWindow( void * hwnd );
GetForegroundWindow value vfx-hwnd
\ vfx-hwnd 0 0 0 0 0 $41 SetWindowPos drop 

: >display ( - )
    flushOP-gen drop \ flush buffered output (VFX)
    reset-keyboard 
    display al_get_win_window_handle SetForegroundWindow drop ;

: >vfx ( - )
    vfx-hwnd SetForegroundWindow drop ;

: fullscreen ( - )
    ." FULLSCREEN" cr -1 to fullscreen? ;

: windowed ( - )
    ." WINDOWED" cr 0 to fullscreen? ;

: *display ( - )
    \ fullscreen? if fullscreen else windowed then
    create-display  going off ;

: winmode ( - )
    2 zoom ! 
    appw apph fullscreen? resize-display
    update-delta update-delta ;

: w/f ( - )
    \ Display mode switching for release builds
    fullscreen? if windowed else fullscreen then
    winmode ;

\ =============================================================================
\ Color and position
\ =============================================================================

\ Color

4 cells ecfield fore
2 cells ecfield pen

: frgba ( f:r f:g f:b f:a - )
    fore 4f! ;
1e 1e 1e 1e frgba

: rgba ( n - )  
    c>f 8 << c>f 8 << c>f 8 << c>f drop frgba ;

\ Palette
include %idir%/vga13h.vfx

: color ( n - )
    \ Indexed color selection
    255 and cells vga13h + @ 8 << $ff or rgba ;

\ Position
: at ( x y - ) pen 2! ;
: at@ ( - x y ) pen 2@ ;
: at@f ( - f:x f:y ) at@ 2s>f ;
: +at ( x y - ) pen 2+! ;

\ =============================================================================
\ Drawing primitives
\ =============================================================================

: albmpw ( albmp - w ) dup if al_get_bitmap_width then ;
: albmph ( albmp - h ) dup if al_get_bitmap_height then ;

: draw-bitmap ( albmp n - )
    over if at@f al_draw_bitmap else 2drop then ;

: *subbmp ( albmp w h - albmp' )
    at@ 2swap al_create_sub_bitmap ?dup 
    0= abort" al_create_sub_bitmap error" ;

: -albmp ( albmp - )
    ?dup -exit al_destroy_bitmap ;

redef : cls ( - )
    0e 0e 0e 1e al_clear_to_color ;

\ =============================================================================
\ Matrix stack
\ =============================================================================

|| create mstk 16 cells 16 * allot
|| create m2 16 cells allot
|| variable mp
|| variable tx variable ty
|| : m mp @ 15 and 16 cells * mstk + ; 

: m{ ( - )
    m 1 mp +! m swap al_copy_transform ;

: m} ( - )
    -1 mp +!  m al_use_transform ;

: transform ( f:x f:y f:sx f:sy a - )
    m2 al_build_transform  m2 m al_compose_transform  m m2 al_copy_transform
    m al_use_transform ;

: identity ( - )
    m al_identity_transform  m al_use_transform ;

: 2p*m ( x. y. - x. y. )
    2p>f ty sf! tx sf!  m tx ty al_transform_coordinates
    tx sf@ f>p ty sf@ f>p ;

: inverse ( - )
    m al_invert_transform ;

\ =============================================================================
\ Text
\ =============================================================================

\ Variables

\ || variable rmargin
variable line-spacing  8 line-spacing !
|| 0 value vga-8x8 \ font

\ Backend

|| create bg.png here
    s" %idir%\builtin\gfx\vga2_8x8_allegro5.png" file,
|| here swap - constant /bg.png

cell ecfield fnt

|| create range(s) 0 , 255 ,

\ Text rendering

: mltext ( a len w - )
    {: | zstr[ 4096 ] lh w z :}
    over 0= if 3drop exit then 
    >r zstr[ zplace  zstr[ r> s>f f>ds   
    line-spacing @ s>f f>ds to lh to w to z 
    fnt @  fore 4f@  at@f w lh 0 z al_draw_multiline_text ;

: print ( a len - )
    winw pen x@ - mltext ;

: print2 ( a len line-spacing - )
    line-spacing @ >r line-spacing !
    print
    r> line-spacing ! ;

: textw ( a n - n )
    {: | zstr[ 4096 ] :}
    zstr[ zplace  zstr[  fnt @  swap  al_get_text_width ;  

: glyph ( n - )
    >r  fnt @ fore 4f@ at@f  r> al_draw_glyph ;

\ Init

: init-vga-font
    bg.png /bg.png 0 z" r" al_open_memfile >r
    r@ 0 al_load_bitmap_f
    r> al_fclose drop
        1 range(s) al_grab_font_from_bitmap
        to vga-8x8 
        vga-8x8 fnt ! ;

\ include %idir%/../textmode/textmode.vfx
