\ ------------------------------------------------------------------------------
\ Engineer OOP extension - Nib 2.0 Foundation
\ ------------------------------------------------------------------------------
\ ==============================================================================
\ [x] Phase 1: Foundation infrastructure 
\ [x] Phase 2: Classifier bit allocation system
\ [ ] Phase 3: Field type registry and protocols
\ [ ] Phase 4: Enhanced message dispatch with classifier checks
\ [ ] Phase 5: Advanced object creation with type validation
\ ==============================================================================

validations off

1024 constant #max-classifiers
4096 constant /classifier

0 value me              \ current object
0 value classifier      \ current classifier
0 value /static         \ next static field offset

variable next-classifier#

\ ==============================================================================
\ Low-level stuff
\ ==============================================================================

magic-id MAGIC_FIELD FIEL
: field ( ofs size - <name> n ) ( a - a )
    create 0 , MAGIC_FIELD , over , +
    does> 2 cells + @ me + ;

: 's ( object - <name> a )
    ' >body 2 cells + @ ?literal s" +" evaluate ; immediate 

: ?already ( ? - b )
    {: n :}
    preword find if
        >body @ n = dup if bl word drop then
    else drop 0 then ; 

|| : ?literal  state @ if postpone literal then ;

: -% ( a c -- a c' )
    dup -exit 2dup + 1 - c@ '%' = -exit 1 - ;

cstring name$
: >$ ( a len - $ )
    name$ place name$ ;

: set-bit ( bit# addr - )
    {: bit# addr | byte-ofs bit-ofs :}
    bit# 8 / to byte-ofs
    bit# 8 mod to bit-ofs
    addr byte-ofs +
    dup c@ 1 bit-ofs lshift or swap c! ;

: test-bit ( bit# addr - b )
    >r 8 /mod ( bit-ofs byte-ofs )
    r> + c@ 1 rot lshift and 0<> ;

\ ==============================================================================
\ Object stack
\ ==============================================================================

: pagealign ( - )  here $ff and 256 swap - allot ;
: aligned-page ( - <name> )  pagealign here $100 allot constant ;
aligned-page os
variable osp   os osp ! \ object stack pointer

\ ==============================================================================
\ Object scoping
\ ==============================================================================

: [[ ( object - ) me osp @ !  4 osp c+!  to me ;
: unsafe-[[ ( a - ) me osp @ !  4 osp c+!  to me ;
: ]] ( - ) -4 osp c+!  osp @ @ to me ;
: you ( - object ) osp @ cell- @ ;
: that ( - object ) osp @ @ ;

\ ==============================================================================
\ Classes
\ ==============================================================================

0
    cell field cla
dup constant /object-base
    cell field magic
constant /magical-base

/magical-base
    cell field instance-size                \ Size of this class
    cell field next-ofs                     \ Next field offset for this class
    cell field field-space-size
    cell field classifier#
    #max-classifiers 8 / field bit-table    \ Classification table
to /static

|| : (classifier) ( class - class )
    here swap , /classifier cell- allot&erase
    [[ /classifier instance-size ! me ]] ;
    
|| : create-classifier ( class - <name> class )
    create (classifier) ;

magic-id CLASS_MAGIC CLAS

create class% class% (classifier) [[ CLASS_MAGIC magic ! ]]
class% create-classifier trait% [[ CLASS_MAGIC magic ! ]]
class% create-classifier extension% [[ CLASS_MAGIC magic ! ]]

\ TODO: implement SETUP TEARDOWN INIT DEINIT with OBJECT% trait added to every object
\ trait% create-classifier object% [[ ]] 

: object ( class - object )
    \ TODO: validate class
    here over , swap 's instance-size @ cell- allot&erase ;

: $create-object ( class name len - object ) ( - object ) 
    >$ $create object ;

: create-object ( class - <name> ) ( - object )
    bl parse $create-object ;

class% create-object var% [[
    /magical-base
        cell field offset
        cell field magic
    dup instance-size ! next-ofs !
]]

|| : has-field-space? ( class - b )
    's field-space-size @ 0<> ;
    
|| : ?no-field-space ( - b )
    classifier has-field-space? not dup if bl word drop then ;

: field-space ( n - )
    classifier [[
        field-space-size !
        field-space-size @ instance-size !
    ]] ;
        
: var ( - <name> ) ( - a )
    ?no-field-space if
        -1 abort" VAR >> Class has no field space defined."
    then
    var% ?already ?exit
    var% create-object [[
        classifier 's next-ofs @ offset !
        cell classifier 's next-ofs +!
        classifier has-field-space? not if            
            next-ofs @ classifier 's instance-size !
        then
        MAGIC_FIELD magic !
    ]]
    does> 's offset @ me + ;  
    
: begin-classifier ( class - )
    create-object to classifier
    next-classifier# @ classifier 's bit-table set-bit
    1 next-classifier# +! ;

: end-classifier ( - )
    0 to classifier ;
    
: class:
    class% begin-classifier
    classifier [[
        /object-base instance-size !
        /object-base next-ofs !
    ]] ;

: class;
    end-classifier ;
    
: is? ( object classifier - bool )
    's classifier# @ swap 's cla @ 's bit-table test-bit ;

class% create-object static% [[
    var% 's instance-size @
    dup instance-size ! next-ofs !
]]

: static ( - <name> ) ( - a )
    static% ?already ?exit    
    static% create-object [[
        /static offset !
        cell +to /static
        MAGIC_FIELD magic !
    ]]
    does> 's offset @ cla @ + ;
    
\ : trait:
\     trait% create-object to classifier ;
\ 
\ : trait;
\     end-classifier ;
\ 
\ : extension:
\     extension% create-object to classifier ;
\ 
\ : extension;
\     end-classifier ;

: init-nib ( - )
    [ next-classifier# @ ] literal next-classifier# ! 
    [ /static ] literal to /static
    0 to classifier ;

\ Tests
\ -----

class: actor%
    512 field-space
    var x
    var y
    var vx
    var vy
class;

actor% create-object test [[
    me .
    x .
    y .
    me 's y .
    me actor% is? .
    cr
    static i/hp
    i/hp . actor% 's i/hp .
]]