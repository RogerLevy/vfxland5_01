\ ------------------------------------------------------------------------------
\ Engineer OOP extension - NIBS (NIBS Interactive Behavior System) a.k.a Nib 2.0
\ ------------------------------------------------------------------------------

\ validations off

1024 constant #max-classifiers
4096 constant /classifier
#max-classifiers 8 / constant /bit-table

0 value me              \ current object
0 value classifier      \ current classifier
0 value /static         \ next static field offset

variable next-classifier#

magic-id MAGIC_FIELD FIEL
magic-id MAGIC_PROPERTY PROP
magic-id MAGIC_CLASSIFIER CLAS

\ ==============================================================================
\ Low-level stuff
\ ==============================================================================

: field ( ofs size <name> - n ) ( a - a )
    create 0 , MAGIC_FIELD , over , +
    does> 2 cells + @ me + ;

0
    cell field cla
dup constant /object-base
    cell field magic
constant /magical-base

: ?prop ( n - n )
    dup 0= abort" ?PROP >> Property offset is 0 - property not mapped for this class!" ;

' cla >body 2 cells + @ constant cla-offset
: prop+ ( object offset - a )
    s" over cla-offset + @ + @ ?prop +" evaluate ; immediate

: 's ( object <name> - a )
    preword find not abort" 'S >> field name not found!"
    >body cell+ @ MAGIC_PROPERTY = if
        ' >body 2 cells + @ ?literal s" prop+" evaluate
    else
        ' >body 2 cells + @ ?literal s" +" evaluate
    then ; immediate 

: already? ( ? <name> - b <name> )
    {: n :}
    preword find if >body @ n = else drop 0 then ;

|| : ?literal  state @ if postpone literal then ;

: -% ( a c -- a c' )
    dup -exit 2dup + 1 - c@ '%' = -exit 1 - ;

cstring name$
: >$ ( a len - $ )
    name$ place name$ ;

: set-bit ( bit# addr - )
    {: bit# addr | byte-ofs bit-ofs :}
    bit# 8 / to byte-ofs
    bit# 8 mod to bit-ofs
    addr byte-ofs +
    dup c@ 1 bit-ofs lshift or swap c! ;

: test-bit ( bit# adr - b )
    >r 8 /mod ( bit-ofs byte-ofs )
    r> + c@ 1 rot lshift and 0<> ;

: add-to ( item list - )
    \ list (head): [ next-node , item , count , ]
    \ node: [ next-node , item , ]
    {: item head :}
    head cell+ cell+ @ 0= if
        item head cell+ !
    else
        head
        begin dup @ while @ repeat 
        here swap ! 0 , item , 
    then
    1 head cell+ cell+ +! ;

: @next ( node - next-node item )
    dup @ swap cell+ @ ;

: list-count ( list - c )
    cell+ cell+ @ ;

: list-count! ( c list - )
    cell+ cell+ ! ;


3 cells constant /list

: copy-list ( src dest - )
    {: src dest :}
    dest /list erase  \ re-initialize head
    src dup list-count for
        @next dest add-to
    loop drop ;

\ ==============================================================================
\ Object stack
\ ==============================================================================

: pagealign ( - )  here $ff and 256 swap - allot ;
: aligned-page ( <name> - )  pagealign here $100 allot constant ;
aligned-page os
variable osp   os osp ! \ object stack pointer

\ ==============================================================================
\ Object scoping
\ ==============================================================================

: [[ ( object - ) me osp @ !  4 osp c+!  to me ;
: unsafe-[[ ( a - ) me osp @ !  4 osp c+!  to me ;
: ]] ( - ) -4 osp c+!  osp @ @ to me ;
: you ( - object ) osp @ cell- @ ;
: that ( - object ) osp @ @ ;
: as> ( actor - ) ( - ) [[ r> execute ]] ;

\ ==============================================================================
\ Classes
\ ==============================================================================

/magical-base
    cell field instance-size                \ Size of this class
    cell field next-ofs                     \ Next field offset for this class
    cell field field-space-size
    cell field classifier#
    /bit-table field bit-table    \ Classification table
    /list field property-list
    /list field protocol-list
    cell field template
    cell field constructor-xt
    cell field constructor-#params
    cell field on-derive-xt
    /list field dependents       \ trait dependents (classes that applied the trait)
    cell field #openings
dup constant /classifier-base
to /static

: allocate-classifier-bit ( - )
    next-classifier# @ classifier 's bit-table set-bit
    next-classifier# @ classifier 's classifier# !
    1 next-classifier# +! ;

|| : (classifier) ( class - class )
    here swap , /classifier cell- allot&erase
    dup to classifier
    [[
        /classifier instance-size !
        allocate-classifier-bit
    me ]]  ;
    
: (init-class) ( - )
    MAGIC_CLASSIFIER magic ! ;
    
create class% class% (classifier) [[ (init-class) ]]

|| : create-class ( <name> - class )
    create class% (classifier) dup [[ (init-class) ]] ;

: sizeof ( class|object - n )
    dup 's cla @ class% = if 's instance-size @
    else 's cla @ 's instance-size @ then ;

\ ==============================================================================
\ TRAIT% and OBJECT%
\ ==============================================================================

: (init-trait) ( - )
    MAGIC_CLASSIFIER magic ! ;

create object% 0 (classifier) [[
    (init-trait)
]]

: (add-object-system-trait-bit) ( - )
    object% 's classifier# @ bit-table set-bit ;

create-class trait% [[
    (add-object-system-trait-bit)
]]

object% [[
    trait% cla !
    (add-object-system-trait-bit)
]]

\ ==============================================================================
\ Object instantiation
\ ==============================================================================

: (sizeof) ( object - n )
    's cla @ 's instance-size @ ;

: !defaults ( object - )
    dup 's cla @ 's template @ ?dup if
        \ dup .name ." template copying" cr
        \ over >r 
        cell+ over dup cell+ swap (sizeof) cell- move
        \ r> dup (sizeof) dump
    else
        dup cell+ over (sizeof) cell- erase
    then
    drop ;

: construct! ( ... class addr - )
    swap over cla-offset + !   \ Store class pointer
    [[
        me !defaults
        cla @ 's constructor-xt @ ?dup if execute then
    ]] ;
    
: construct ( ... class - object )
    \ TODO: validate class
    here >r
    dup 's instance-size @ allot
    ( class ) r@ construct! r> ;

: $object ( ... class name len - object ) ( - object ) 
    >$ $create construct ;

: object ( ... class <name> - ) ( - object )
    bl parse $object drop ;

\ ==============================================================================
\ Members
\ ==============================================================================

create property%  class% construct [[
    (add-object-system-trait-bit)
    /magical-base
        cell field offset
        cell field next-property
        cell field property-size
    dup instance-size ! next-ofs !
]]

create static%  class% construct [[
    property% 's instance-size @
    dup instance-size ! next-ofs !
]]

create protocol%  class% construct [[
    static% 's instance-size @
        cell field default-xt
        cell field next-protocol
    dup instance-size ! next-ofs !
]]

|| : has-field-space? ( class - b )
    's field-space-size @ 0<> ;
    
: field-space ( n - )
    classifier 's cla @ trait% = abort" FIELD-SPACE >> Cannot add a field-space to traits, only classes."
    classifier [[
        field-space-size !
        field-space-size @ instance-size !
    ]] ;       

: begin-classifier ( class - )
    push-order public
    create construct to classifier
    1 classifier 's #openings +!
    allocate-classifier-bit
    pop-order ;

: end-classifier ( - )
    0 to classifier ;
    
: reopened? ( class - b )
    's #openings @ 1 > ;

: allocate-static-offset ( - )
    /static offset !
    cell +to /static ;
    
: create-property ( size <name> - property )
    push-order public
    create property% construct [[
        property-size !
        allocate-static-offset
        MAGIC_PROPERTY magic !
    me ]]
    pop-order
    does> 's offset @ cla @ + @ ?prop me + ;

: defining-a-trait? ( - b )
    classifier 's cla @ trait% = ;

: add-property ( property - )
    classifier 's property-list add-to ;

: is-property-mapped? ( property - b )
    's offset @ classifier + @ 0<> ;

: map-property ( property - )
    \ map property for current class
    [[
        classifier 's next-ofs @ offset @ classifier + !
        property-size @ classifier 's next-ofs +!
    ]]
    classifier has-field-space? not if            
        classifier [[ next-ofs @ instance-size ! ]]
    then ;

: supports-protocol? ( protocol class - bool )
    swap 's offset @ + @ 0<> ;

: support-protocol ( protocol class - )
    >r [[ default-xt @ offset @ ]] r> + ! ;

: has-unmapped-properties? ( trait - bool )
    {: trait :}
    trait 's property-list dup list-count for
        @next ( property ) is-property-mapped? not if
            -1 leave
        then
    loop drop
    0 ;

: ?size-changed ( size property - property )
    {: size property :}
    property 's property-size @ size <> if
        -1
        size
        property 's property-size @
        property body> >name count
        f" NPROPERTY >> %s changed from %n to %n bytes - reload/restart required."
        .abort
    then
    property ;

: nproperty ( size <name> - ) ( - a )
    classifier 0= abort" NPROPERTY >> Can't define a property outside of a classifier block."

    property% already? if
        ' >body ?size-changed
    else
        \ Property doesn't exist - check rules
        classifier 's cla @ trait% = if
            \ Trait rules
            classifier reopened? if
                classifier 's dependents list-count 0<>
                abort" NPROPERTY >> Cannot add new properties to reopened traits with dependents."
            then
            \ New trait or reopened trait with no dependents - okay
        else
            \ Class rules
            classifier reopened? if
                \ Reopened class without a field-space - abort
                \ (Probably won't ever happen because CLASS: redefines
                \ classes without field-spaces instead of re-opening them)
                classifier has-field-space? not
                abort" NPROPERTY >> Cannot add properties to an existing class without a field-space."
                \ Reopened class with a field-space - okay
            then
            \ New class - okay
        then
        create-property
    then
    
    \ Map the property
    ( property )
    dup is-property-mapped? if
        drop \ -1 abort" PROPERTY >> Property already mapped!"
    else
        dup add-property
        map-property
    then ;
    
: property ( <name> - ) ( - a )
    cell nproperty ;

: static ( <name> - ) ( - a )
    classifier 0= abort" STATIC >> Can't define a static outside of a classifier block."
    static% already? if ' drop exit then
    push-order public
    create static% construct [[
        allocate-static-offset
        MAGIC_FIELD magic !
    ]]
    pop-order
    does> 's offset @ cla @ + ;

\ ==============================================================================
\ Classifier definition
\ ==============================================================================

: apply-trait ( trait class - )
    {: trait class :}
    class 's cla @ class% <> abort" IS-A >> Invalid destination - must be a class."
    
    \ Check if trait has NEW properties that class can't handle
    class reopened? if
        class has-field-space? not if
            classifier >r class to classifier
            trait has-unmapped-properties? 
            r> to classifier
            abort" APPLY-TRAIT >> Cannot add trait with new properties to reopened class without field-space."
        then
    then
    
    \ Set classifier bit
    trait 's classifier# @ class 's bit-table set-bit
    
    \ Merge protocols
    trait 's protocol-list dup list-count for
        @next \ ++dup .name
        ( protocol ) class support-protocol 
    loop drop
    
    \ Merge properties
    trait 's property-list dup list-count for
        @next dup is-property-mapped? if drop else map-property then
    loop drop
    
    \ Add class to dependents
    class trait 's dependents add-to ;
   
: class: ( <name> - )
    class% already? if
        ' >body to classifier
        exit
    then
    class% begin-classifier
    classifier [[
        (init-class) 
        /object-base instance-size !
        /object-base next-ofs !
        object% classifier apply-trait 
    ]] ;

: class; ( - )
    end-classifier ;

: is-a ( [class] <trait> - )
    \ [class] only required if outside class block
    ' >body classifier ?dup 0= if swap then apply-trait ;
    
: is? ( object classifier - bool )
    's classifier# @ swap 's cla @ 's bit-table test-bit ;

: trait: ( <name> - )
    trait% already? if
        ' >body to classifier
        exit
    then
    trait% begin-classifier
    classifier [[
        (init-trait)
    ]] ;

: trait;
    end-classifier ;

: add-protocol ( protocol trait - )
    's protocol-list add-to ;

defer (on-exec-error)

debug @ [if]
    : oexec ( ... xt - ... )
        ?dup -exit
        {: xt :}
        xt catch ?dup if
            xt swap (on-exec-error)
        then ;
[else]
    : oexec ( ... xt - ... )
        ?dup -exit >r ;
[then]

: does-protocol ( - )
    does> swap [[
        dup 's offset @ cla @ + @ ?dup if
            nip
        else
            ( protocol ) >r
            -1
            cla @ >name count
            r> body> >name count
            f" DOES-PROTOCOL >> Protocol %s not supported by %s!" .abort
        then
        ( xt ) oexec
    ]] ;

: implement-protocol ( protocol class - )
    {: protocol class | name[ 256 ] :} 
    protocol [[
        me body> >name count name[ place
        name[ count
            class body> >name count 1 -
            f" : %s::%s" 2dup upper evaluate
        state on
        here ( xt ) class offset @ + !
    ]] ;
    
: (protocol:) ( trait <name> - ) ( ... object - ... )
    {: trait | protocol class :}
    protocol% already? if
        ' >body
        \ TODO: check if the protocol belongs to another trait and throw error if it is
    else
        create protocol% construct
        does-protocol 
        [[
            me trait add-protocol
            allocate-static-offset 
        me ]]
    then
    to protocol
    :noname protocol 's default-xt !
    
    \ Propagate the protocol to dependents
    trait 's dependents dup list-count for
        @next to class
        protocol class supports-protocol? not if
            protocol class support-protocol
        then
    loop drop
    ;

: :: ( classifier <protocol> - )
    {: class | protocol :}
    classifier abort" :: >> Can only define a protocol outside of a classifier block."
    class 's cla @ trait% = if class (protocol:) exit then
    class 's cla @ class% <> abort" :: >> Invalid protocol implementation destination. (Must be a class.)"
    bl word find not abort" :: >> Protocol not found. Note: Protocols can only be created on traits."
    >body to protocol
    protocol 's cla @ protocol% <> abort" :: >> Word is not a protocol!"
    protocol class supports-protocol? not abort" :: >> Tried to implement a protocol not supported by the class."
    protocol class implement-protocol ;

: stub ( - )
    -1 r> >name count f" %s >> Not implemented" .abort ;

: :construct ( #params class - )
    classifier abort" :CONSTRUCT >> Constructors can only be defined outside of a classifier block."
    dup 's cla @ class% <> abort" :CONSTRUCT >> Passed object is not a class - constructors can only be defined for classes."
    [[
        :noname constructor-xt !
        constructor-#params !
    ]] ;

\ : defaults ( - a )
\     classifier 0= abort" DEFAULTS >> Can't define defaults outside of a class block."
\     classifier 's cla @ trait% = abort" DEFAULTS >> Cannot create defaults for traits - use in classes only"
\     classifier 's template @ ?dup if else
\         classifier construct classifier 's template !
\         classifier 's template @
\     then ;

: defaults ( class - a )
    {: class :}
    class 's cla @ trait% = abort" DEFAULTS >> Traits cannot have defaults. (Only classes.)"
    class 's template @ ?dup 0= if
        class construct class 's template !
        class 's template @
    then ;

: derive ( class - )
    {: source-class :}
    
    source-class 's cla @ class% <> abort" DERIVE >> Can only derive from classes."
    classifier 0= abort" DERIVE >> Can only be called inside a class block."
    
    \ Copy size fields
    source-class [[ instance-size @ next-ofs @ field-space-size @ ]]
    classifier [[ field-space-size ! next-ofs ! instance-size ! ]]
    
    \ Copy statics
    source-class /classifier-base +
    classifier /classifier-base +   
    /classifier /classifier-base - move
    
    \ Copy member lists
    source-class 's protocol-list  classifier 's protocol-list copy-list
    source-class 's property-list  classifier 's property-list copy-list
    
    \ Copy parent's bit-table (trait relationships)
    source-class 's bit-table classifier 's bit-table /bit-table move
    
    \ Set parent bit for inheritance queries
    source-class 's classifier# @ classifier 's bit-table set-bit
    
    \ Copy template if it exists
    source-class 's template @ ?dup if
        classifier defaults source-class 's instance-size @ move
    then
    
    \ Execute on-derive if set
    source-class 's on-derive-xt @ ?dup if
        source-class classifier rot execute  \ ( source-class derived-class - )
    then 
;

: :derive ( class - ) ( source-class derived-class - )
    dup 's cla @ class% <> abort" :DERIVE >> Can only define on-derive hooks for classes! (Did you pass a trait?)"
    [[ :noname on-derive-xt ! ]] ;

\ ==============================================================================
\ Misc
\ ==============================================================================

aka nprop nproperty
aka prop property

\ Validation
: valid-class? ( a - n:res )
    dup firstname here within  >r  \ in dictionary
    r@ 0< if
        dup 's cla @ class% = r> and >r
    then
    drop r> ;

: valid-object? ( a - n:res )
    dup firstname here within  >r  \ in dictionary
    r@ 0< if
        dup 's cla @ ( class ) valid-class? r> and >r
    then
    drop r> ;

: copy ( a1 a2 - a2 )
    dup dup >r sizeof move r> ;

\ ==============================================================================
\ Object% Base Protocols
\ ==============================================================================

0 to classifier

object% :: info ( - )
    \ Default inspection - show class name and address
    me . me .name ;

object% :: describe ( - )
    \ Detailed inspection - override in subclasses for specific info
    me info ;

object% :: on-exec-error ( xt error# - )
    throw ;
    
' on-exec-error is (on-exec-error)

\ ==============================================================================
\ Init/Reset
\ ==============================================================================

: reset-object-trait ( - )
    [ object% 's protocol-list list-count ] literal
    object% 's protocol-list list-count!
    [ object% 's property-list list-count ] literal
    object% 's property-list list-count! ;

: init-nib ( - )
    [ next-classifier# @ ] literal next-classifier# ! 
    [ /static ] literal to /static
    0 to classifier
    reset-object-trait ;
