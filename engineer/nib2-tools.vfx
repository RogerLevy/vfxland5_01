\ ------------------------------------------------------------------------------
\ Nib 2.0 Introspection Tools
\ Claude wrote the initial version of this but it's been heavily modified
\ ------------------------------------------------------------------------------

|| 0 value target-class
|| 0 value target-bit
|| 0 value found-items
    
|| : find-parent ( thread# nfa - f )
    {: thread# nfa | body :}
    nfa name> >body to body
    body 's cla @ _class = if
        body 's classifier# @ target-bit = if
            ."        - " nfa count type cr 
            0  \ found it, stop walking
            1 +to found-items
        else -1 then
    else -1 then ;
    
|| : .class-parents ( class - )
    \ Print all ancestors of a class
    dup valid-class? not abort" Not a valid class."
    
    to target-class
    cr ."     Parents of " target-class body> >name count type ." :" cr
    
    0 to found-items
    #max-classifiers for
        i target-class 's can-table test-bit if
            \ Check if this bit represents a trait (not the class itself)
            i target-class 's classifier# @ <> if
                \ Find which trait has this classifier#
                \ This is a bit hacky - we walk the dictionary looking for traits
                i to target-bit
                ['] find-parent forth-wordlist WalkWordList
            then
        then
    loop
    
    found-items 0= if ."        (no parents derived)" cr then ;

|| : find-trait ( thread# nfa - f )
    {: thread# nfa | body :}
    nfa name> >body to body
    body 's cla @ _trait = if
        body 's classifier# @ target-bit = if
            ."        - " nfa count type
            target-bit target-class 's id-table test-bit 0= if
                ."  (capability only)"
            then
            cr 
            0  \ found it, stop walking
            1 +to found-items
        else -1 then
    else -1 then ;

|| : .class-traits ( class - )
    \ Print all traits applied to a class
    dup valid-class? not abort" Not a valid class."
    
    to target-class
    cr ."     Traits of " target-class body> >name count type ." :" cr
    
    0 to found-items
    #max-classifiers for
        i target-class 's can-table test-bit if
            \ Check if this bit represents a trait (not the class itself)
            i target-class 's classifier# @ <> if
                \ Find which trait has this classifier#
                \ This is a bit hacky - we walk the dictionary looking for traits
                i to target-bit
                ['] find-trait forth-wordlist WalkWordList
            then
        then
    loop
    
    found-items 0= if ."        (no traits applied)" cr then ;

: .class ( class - )
    \ Print detailed information about a class
    {: class0 :}
    class0 valid-class? not if ." Not a valid class" exit then
    
    cr
    ."     Class: " class0 body> >name count type cr
    ."     Instance size: " class0 's instance-size @ . ." bytes" cr
    ."     Field-space: " class0 's field-space-size @ ?dup if . ." bytes" else ." none" then cr
    ."     Classifier#: " class0 's classifier# @ . cr
    ."     Properties: " class0 's property-list list-count . cr
    ."     Protocols: " class0 's protocol-list list-count . cr
    ."     Dependents: " class0 's dependents list-count . cr
    
    class0 .class-traits
    class0 .class-parents ;

|| : ?.class ( thread# nfa - f )
    {: thread# nfa | body :}
    nfa name> >body to body
    body 's cla @ _class = if
        ."        - " nfa count type cr
        1 +to found-items        
    then -1 ;

|| : ?.trait ( thread# nfa - f )
    {: thread# nfa | body :}
    nfa name> >body to body
    body 's cla @ _trait = if
        ."        - " nfa count type cr
        1 +to found-items        
    then -1 ;

: .nibs ( - )
    cr
    ."    Classes:" cr
    0 to found-items
    ['] ?.class forth-wordlist WalkWordList 
    ."        Total: " found-items . cr cr
    ."    Traits:" cr
    0 to found-items
    ['] ?.trait forth-wordlist WalkWordList 
    ."        Total: " found-items . cr ;
    

\ Example usage:
\ ship% .class