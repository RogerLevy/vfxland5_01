\ ------------------------------------------------------------------------------
\ Allegro Utilities
\ ------------------------------------------------------------------------------
public

variable zoom  2 zoom !
320 value appw
240 value apph
0 value winw
0 value winh
0 value monw
0 value monh
0 value fullscreen?
0 value display

ALLEGRO_OPENGL ALLEGRO_PROGRAMMABLE_PIPELINE or
    value display-flags

0 value builtin-font  \ builtin-font

create kbs0 /ALLEGRO_KEYBOARD_STATE allot&erase
create kbs1 /ALLEGRO_KEYBOARD_STATE allot&erase
create ms0 /ALLEGRO_MOUSE_STATE allot&erase
create ms1 /ALLEGRO_MOUSE_STATE allot&erase

0 value frame-queue
0 value frame-timer
60e fvalue tps \ float, ticks (frames) per second

\ ------------------------------------------------------------------------------

private

0 value mixer
0 value voice
0 value queue

create mi /ALLEGRO_MONITOR_INFO allot&erase

: -audio  ( -- )
    mixer 0= if exit then
    mixer 0 al_set_mixer_playing drop ;

: +audio  ( -- )
    mixer if  mixer 1 al_set_mixer_playing drop  exit then 
    48000 ALLEGRO_AUDIO_DEPTH_INT16 ALLEGRO_CHANNEL_CONF_2 al_create_voice to voice
    48000 ALLEGRO_AUDIO_DEPTH_FLOAT32 ALLEGRO_CHANNEL_CONF_2 al_create_mixer to mixer
    mixer voice al_attach_mixer_to_voice 0= abort" Couldn't initialize audio"
    mixer al_set_default_mixer drop
    mixer 1 al_set_mixer_playing drop ;

: check  0= abort" Allegro init error" ;

\ ------------------------------------------------------------------------------

public

: reset-keyboard  ( -- )
    kbs0 @ if \ check for display pointer
        kbs0 al_clear_keyboard_state 
        kbs1 al_clear_keyboard_state
    then
    al_uninstall_keyboard  
    al_install_keyboard drop
    queue al_get_keyboard_event_source al_register_event_source ;

: /frame-timer ( - )
    1e tps f/ al_create_timer to frame-timer
    al_create_event_queue to frame-queue
    frame-queue` frame-timer al_get_timer_event_source` al_register_event_source
;

: init-allegro  ( -- )    
    ALLEGRO_VERSION 0 al_install_system check
    
    al_init_image_addon check
    al_init_native_dialog_addon check
    al_init_primitives_addon check
    al_init_font_addon
    al_init_ttf_addon check
    al_init_acodec_addon check
    al_install_audio 0= abort" Error installing audio."
    al_install_haptic check
    al_install_joystick check
    al_install_mouse check
    al_install_touch_input check
    
    0 pad al_get_monitor_info 0= throw
    pad 2 cells + @  pad @  - to monw
    pad 3 cells + @  pad cell+ @  - to monh

    0 to mixer  0 to voice
    64 al_reserve_samples 0= abort" Allegro: Error reserving samples." 
    +audio

    al_create_builtin_font to builtin-font

    /frame-timer
;

: create-display
    ALLEGRO_SINGLE_BUFFER  0  ALLEGRO_SUGGEST  al_set_new_display_option
    \ ALLEGRO_SWAP_METHOD  1  ALLEGRO_SUGGEST  al_set_new_display_option
    ALLEGRO_VSYNC  1  ALLEGRO_SUGGEST  al_set_new_display_option
    
    fullscreen? if
        monw to winw  monh to winh
    else
        appw zoom @ * to winw  apph zoom @ * to winh
    then
    
    display-flags
        fullscreen? if ALLEGRO_FULLSCREEN_WINDOW or then
        
        ALLEGRO_GENERATE_EXPOSE_EVENTS or
        al_set_new_display_flags
    
    winw winh al_create_display to display
        
    al_create_event_queue to queue
    queue  display       al_get_display_event_source  al_register_event_source
    queue                al_get_mouse_event_source    al_register_event_source

    ALLEGRO_ADD ALLEGRO_ALPHA ALLEGRO_INVERSE_ALPHA al_set_blender

    reset-keyboard  \ depends on queue
;

: resize-display ( w h fullscreen? - )
    dup to fullscreen?
    if
        to apph to appw
        monw to winw  monh to winh
    else
        2dup to apph to appw
        zoom @ * to winh zoom @ * to winw
        display winw winh al_resize_display 0= if
            ." RESIZE-DISPLAY : al_resize_display failed!" cr 
        then 
    then 
    display
    ALLEGRO_FULLSCREEN_WINDOW
    fullscreen? 1 and
    al_set_display_flag 0= if
        ." RESIZE-DISPLAY : al_set_display_flag failed!" cr 
    then ;

: poll-keyboard  ( -- )
    kbs0 kbs1 /ALLEGRO_KEYBOARD_STATE move
    kbs0 al_get_keyboard_state ;

: reset-mouse
    ms0 /ALLEGRO_MOUSE_STATE erase
    ms1 /ALLEGRO_MOUSE_STATE erase ;

: poll-mouse  ( -- )
    ms0 ms1 /ALLEGRO_MOUSE_STATE move
    ms0 al_get_mouse_state ;

: file-dialog  ( zpath ztitle zformats - a n ) 
    al_create_native_file_dialog >r  
    display r@ al_show_native_file_dialog drop
    r@ al_get_native_file_dialog_count if
        r@ 0 al_get_native_file_dialog_path zcount 
    else 0 then
    r> al_destroy_native_file_dialog ;

aka alqueue queue
aka almixer mixer 