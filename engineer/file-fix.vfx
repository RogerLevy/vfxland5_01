\ ------------------------------------------------------------------------------
\ Fixed file loading words with absolute path canonicalization
\ Shadows VFX Forth's include/require to fix relative path tracking bug
\ ------------------------------------------------------------------------------

only forth definitions also system

\ Path canonicalization utilities
create abs-path-buf$  256 allot
create temp-path-buf$ 256 allot

: >abspath ( c-addr u -- c-addr2 u2 )
    \ Convert any path to absolute canonical form using Windows API
    2dup temp-path-buf$ zplace
    temp-path-buf$ 256 abs-path-buf$ 0 GetFullPathName
        0> if
            2drop abs-path-buf$ zcount
        then ;

: normalize ( c-addr u -- c-addr2 u2 )
    >abspath ;           \ Convert to canonical absolute path

: .s
    depth 8 min locals| d |
    d 0< -4 ?throw
    d if ."     DS( " d . ." ):[ " else ."     DS:[ " then
    d if
        d 0 ?do d 1 - i - pick . loop 
    else
        ." Empty! "
    then
    ." ] " cr ;

: ending ( a n c - a n )
   >r begin  2dup r@ scan
      ?dup while  2swap 2drop  #1 /string
   repeat  r> 2drop ;
: slashes  2dup  over + swap do  i c@ '/' = if
    '\' i c!  then  #1 +loop ; 
: -path ( a n - a n ) slashes  '\' ending  0 max ;

\ Fixed file loading words
: included      \ c-addr u --
    {: fn len | file sfstruct err sp :}
    EmptyIdle
    fn len expand normalize to len to fn
    ."     Including " fn len -path type cr 
    fn len AddSourceFile -rot to len to fn    \ Store canonical name
    fn len r/o ?Relative-Open-File #-258 ?throw to file to sfstruct
    fn len ++IncDir
    sp@ to sp
    sfstruct file (include-file) to err
    file close-file drop
    --IncDir
    err throw
    base @ to SbBase
    depth to SbDepth
    context @ to sbContext
    current @ to sbCurrent
    sp sp! ;

: include       \ "<name>" --
    bl parse included ;

: required      \ c-addr u --
    expand
    normalize          \ Get canonical filename
    2dup ['] SourceFiles voc>wid search-wordlist
        if  drop 2drop  else  included  then ;

: require       \ "<name>" --
    bl parse required ;

\ Test word to show what gets stored

: (.source)  cr nip count type -1 ;

: .sources   
    cr cr ." Source Files in Current Build:"
    ['] (.source) ['] SourceFiles >body @ WalkWordList ;
