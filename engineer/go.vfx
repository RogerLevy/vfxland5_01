\ =============================================================================
\ Main Loop Support
\ =============================================================================

|| : pre ( - )
    0 #tib ! postpone \\ 
    update-delta update-delta 
    need-pump on ;
    
continuation: show> ( - ) ( - )
    ( xt ) 'show ! cls ; 

: poll ( - )
    poll-keyboard  
    <alt> held? <altgr> held? or to alt? 
    <lshift> held? <rshift> held? or to shift? 
    <lctrl> held? <rctrl> held? or to ctrl? ;

: spin ( - f: n )
    ustime  ucounter 2dup to ustime  2swap 2- d>f 1000000e f/ ;

\ =============================================================================
\ Global Events
\ =============================================================================

|| : global-events ( - )
    ALLEGRO_EVENT_DISPLAY_CLOSE happened? if
        bye
    then
    ALLEGRO_EVENT_DISPLAY_SWITCH_OUT happened? if
        reset-keyboard reset-mouse
    then
    ALLEGRO_EVENT_DISPLAY_RESIZE happened? if
        display al_acknowledge_resize
    then ;

\ =============================================================================
\ Global Event Handler
\ =============================================================================

|| : ?event ( - flag )
    alqueue the-event al_get_next_event 0<> dup -exit
    >r global-events ide-events r> ;
    
|| : (pump) ( - )
    need-pump @ -exit
    begin ?event not until ;

: pump ( - )
    ['] (pump) catch hist[ .throw hist]
    need-pump on ;

redef : event? ( - f )
    repl @ if 0 exit then
    need-pump off ?event ;

\ =============================================================================
\ Main Loop
\ =============================================================================

|| : ?idle ( - ) debug @ if EmptyIdle then ;

: refresh ( - )
    ?idle 
    ustime 
    0e begin spin f+ fdup 1e tps f/ f>= until fdrop
    to ustime
    display al_flip_display
    update-delta
    pump 
    poll ;

|| : ?quit
    <esc> pressed? esc-quits @ and if
        going off 
        fullscreen? if windowed winmode then
        >vfx
        quit
    then ;

|| : ?reload-shift ( - ) shift? to reload-shift? ;
|| : ?w/f ( - )
    debug @ if ide-?w/f exit then
    <enter> pressed?  alt? and if
        w/f -keys
    then ;    
|| : ?bye  <f4> pressed?  alt? and if bye then ;
|| : controls ( - ) ?quit ?w/f ?bye ?reload-shift ;
|| : screen ( - ) debug @ if draw-ide else ['] draw-game draw-tv then ;

: frame ( - )
    controls screen refresh ;

extern BOOL SetThreadPriority( hthread, priority );

: go ( - )
    \ Main engine loop - enter IDE if debug mode, otherwise run normally
    GetCurrentThread 2 SetThreadPriority drop
    going @ ?exit
    debug @ if >ide else >display then
    pre going on begin ['] frame catch ?dup until going off throw ;

