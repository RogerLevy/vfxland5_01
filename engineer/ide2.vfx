\ ------------------------------------------------------------------------------
\ IDE2 - Next-Level Development Interface  
\ ------------------------------------------------------------------------------

private   
include %idir%/history.vfx   \ private to this file's vocabulary

: toggle dup @ 0= swap ! ;
synonym dup-endcase end-case
aligned-page uios
variable uiosp  uios uiosp !
variable display-mode  \ 0 = IDE, 1 = Fullscreen (game), 2 = Windowed (game)

: ?type ?dup 0= if drop else type then ;

create scalexy

: scaled>  ( x. y. - <code> ; )
    2dup scalexy 2! +m identity 0e 0e 2p>f 0e transform r> execute -m ;

: scaled-at ( x y - )
    scalexy 2@ 2p/ at ;

\ =============================================================================
\ IDE Components Display
\ =============================================================================

variable active  active on
variable stackmode
stackmode jumptable ?.
    ' . , ' p. , ' h. , ' .name ,

\ : win/ ( x. y. - x y )
\     winw winh 2swap 2p* ;
\ 
\ : pos ( x. y. - )
\     \ Position the pen proportionally to the display
\     win/ at ;

variable hsplit   0.5 hsplit !

: split-x ( - x )
    winw hsplit @ p* ;

: from-bottom ( y - y )
    winh swap - ;

: lines ( n - h )
    24 * ;

borrow engineer~ draw-game
: game-window ( - )
    2. 2. scaled>
    0 0 640 480 al_set_clipping_rectangle
    draw-game
    0 0 winw winh al_set_clipping_rectangle ;

borrow engineer~ func[]
: .hotkeys ( - )
    s" Alt+Enter=Display  Tab=REPL  Esc=Quit  Alt+F4=Exit" type cr
    12 for
        i func[] c@ if
            i func[] count 2dup upper i 1 + f" F%n=%s " type
        then
    loop ;

: .honing ( - )
    honing @ 0= if ." OFF " exit then
    honing @ if
        honing @ -1 = if
            retry-filepath$ count ?type
        else
            honing @ .name
        then
    then ;

: .state ( - )
    state @ if ." COMPILING" else ." INTERPRETING" then ;

: .watch ( - )
    watch-xt @ -exit
    watch$ count ?type cr
    s[ watch-xt @ catch drop s] ?type ;

: info-box ( - )
    0 600 scaled-at
    s[
        .hotkeys cr
        ." CWD: " path '\' ending type cr
        ." Me: " .me cr
        ." Context: " context @ .name ." Current: " current @ .name cr
        ." Honing: " .honing cr
        ." State: " .state cr
        ." Watch: " .watch cr
    s] 12 print2 ;

: user-area ( - )
    0 600 8 lines + scaled-at
    s" (User Area)" print ;

: command-history ( - )
    1. 2. scaled>
    split-x 0 at
    monh 7 lines - 24 / visible-lines !
    s[ render-history s] 12 print2 ;

: ints  0 stackmode ! ;
: fixes  1 stackmode ! ;
: hexes  2 stackmode ! ;
: names  3 stackmode ! ;

: ?.s ( - )
    depth | d |
    d 0 max 0 ?do d 1 - i - pick ?. loop
    cr ; 

: stack-display ( - )
    1. 2. scaled>
    split-x 6 lines from-bottom scaled-at
    s[  
        ints ?.s
        fixes ?.s
        hexes ?.s
        names ?.s
        \ f.s
    s] 12 print2 ;

: .prompt ( - ) 
    context @ .name
    ." > "  tib #tib @ ?type ;

: command-line ( - )
    1. 2. scaled>
    split-x 1 lines from-bottom scaled-at
    s[
        .prompt
        active @ repl @ and if
            counter 20 / 8 and if ." _" then
        then
    s] print ;
    
: ide-layout ( - )
    \ title-bar
    game-window     command-history
    info-box        stack-display
    user-area       command-line ;

\ =============================================================================
\ History I/O device
\ =============================================================================

create str 32768 allot
0 value >str

: hist-emit  drop  str >str + c!  1 +to >str ;
: hist-type  drop  bounds ?do  i c@ 0 hist-emit  loop ;
: hist-cr    drop  10 0 hist-emit ;

create vectors
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' hist-emit ,
  ' drop ,
  ' hist-type ,
  ' hist-cr ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
  ' drop ,
create hist-dev   0 , vectors ,
0 value old-dev
: hist[  0 to >str  op-handle @ to old-dev  hist-dev op-handle ! ;
: hist]  old-dev op-handle !  str >str add-history ;

\ =============================================================================
\ IDE Logic
\ =============================================================================

: obey ( - )
    osp @ >r uiosp @ osp !    
    s[ .prompt s] add-history 
    hist[ tib #tib @ ['] evaluate catch 0 #tib ! hist]
    osp @ uiosp ! r> osp !    
    dup .throw-catch if rs state off then ; \ throw ;

: ?charkey ( - )
    alt? if drop exit then
    case
        ( bksp ) 8 of #tib @ -exit -1 #tib +! exit endof
        ( tab ) 9 of exit endof
        ( enter ) 13 of alt? not if obey then exit endof
        ( esc ) 27 of exit endof
    dup-endcase
    dup 32 < over 127 = or if drop exit then
    tib #tib @ + c! 1 #tib +! ;

: repl-events ( - )
    ALLEGRO_EVENT_KEY_DOWN happened? if
        the-key <tab> = if
            repl toggle
        then
        the-key <`> = if
            ide @ not if
                repl toggle
            then
        then
    then
    ALLEGRO_EVENT_DISPLAY_SWITCH_OUT happened? if
        active off
    then
    ALLEGRO_EVENT_DISPLAY_SWITCH_IN happened? if
        active on
    then
    repl @ if
        ALLEGRO_EVENT_KEY_CHAR happened? if
            the-char ?charkey    
        then
    then ;

\ =============================================================================
\ Display Modes
\ =============================================================================

borrow engineer~ fullscreen
borrow engineer~ windowed
borrow engineer~ winmode
borrow engineer~ draw-tv

: ide-mode ( - )
    fullscreen winmode
    ide on repl on ;

: full-mode ( - )
    fullscreen winmode
    ide off repl off ;
    
: windowed-mode ( - )
    windowed winmode
    ide off repl off ;

display-mode jumptable enact-display-mode
    ' ide-mode ,
    ' full-mode ,
    ' windowed-mode ,

: ide-mode-draw ( - )
    2. 2. scaled>
    pen 2@ 2>r ide-layout 2r> at ;

: full-mode-draw ( - )
    draw-tv
    ( draw repl here )
    ;
    
: windowed-mode-draw ( - )
    ?border draw-game
    ( draw repl here ) ;

display-mode jumptable draw-display-mode
    ' ide-mode-draw ,
    ' full-mode-draw ,
    ' windowed-mode-draw ,

\ =============================================================================
\ Public Words
\ =============================================================================

borrow engineer~ -keys

public

: ?w/f ( - )
    <enter> pressed?  alt? and if
        \ Toggle between 3 states: windowed, fullscreen (IDE), and fullscreen (game)
        display-mode @ 1 + 3 umod display-mode !
        enact-display-mode
        -keys
    then ;

: ide-events ( - )
    debug @ if
        ALLEGRO_EVENT_KEY_DOWN happened? if
            the-key ?func
        then
        repl-events
    then ;

: draw-ide ( - )
    cls
    draw-display-mode ;

: init-ide ( - )
    init-history 
    0 display-mode !
    enact-display-mode ;

\ ???
' noop is boot
' noop is onSwitchIn
