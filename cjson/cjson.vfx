library: cjson.dll

vocabulary cjson>>

private

0 constant cJSON_Invalid
1 constant cJSON_False
2 constant cJSON_True
4 constant cJSON_NULL
8 constant cJSON_Number
16 constant cJSON_String
32 constant cJSON_Array
64 constant cJSON_Object

extern: void * STDCALL _cJSON_Parse@4( char * json_string );
extern: void * STDCALL _cJSON_ParseWithLength@8( char * json_string, int buffer_length );
extern: void * STDCALL _cJSON_GetObjectItem@8( void * json_obj, char * key );
extern: char * STDCALL _cJSON_GetStringValue@4( void * item );
extern: void STDCALL _cJSON_Delete@4( void * json_obj );
extern: void * STDCALL _cJSON_CreateObject@0();
extern: void * STDCALL _cJSON_AddStringToObject@12( void * obj, char * key, char * value );
extern: char * STDCALL _cJSON_Print@4( void * obj );
extern: char * STDCALL _cJSON_PrintUnformatted@4( void * obj );
extern: char * STDCALL _cJSON_Version@0();
extern: void STDCALL _cJSON_free@4( void * ptr );
extern: void * STDCALL _cJSON_GetArrayItem@8( void * array, int index );
extern: int STDCALL _cJSON_GetArraySize@4( void * array );
extern: void * STDCALL _cJSON_AddNumberToObject@16( void * obj, char * key, double num );
extern: double STDCALL _cJSON_GetNumberValue@4( void * item );
extern: bool STDCALL _cJSON_AddItemToObject@12( void * obj, char * zname, void * item );
extern: void * STDCALL _cJSON_CreateArray@0();
extern: bool STDCALL _cJSON_AddItemToArray@8( void * array, void * item );
extern: void * STDCALL _cJSON_CreateNumber@8( double num );
extern: void * STDCALL _cJSON_CreateString@4( char * zstr );

: .cjson-version ( - ) _cJSON_Version@0 zcount type cr ;

: cjson-delete ( cjson - ) _cJSON_Delete@4 ;
: new-object ( - cjson ) _cJSON_CreateObject@0 ;
: new-array ( - cjson ) _cJSON_CreateArray@0 ;
: new-number ( f:n - item ) _cJSON_CreateNumber@8 ;
: new-string ( zstr - item ) _cJSON_CreateString@4 ;
: get-type ( cjson - type ) 3 cells + @ ;
: is-array? ( cjson - flag ) get-type cJSON_Array = ;

: serialize-cjson ( cjson - heap-zstr len ) _cJSON_Print@4 zcount ;
: cjson-free ( heap-zstr - ) _cJSON_free@4 ;


\ Generic add-item that works for both arrays and objects
: add-item ( item zkey|0 cjson - )
    dup is-array? if
        nip \ drop key
        swap _cJSON_AddItemToArray@8 drop
    else
        -rot swap _cJSON_AddItemToObject@12 drop
    then ;

: add-object ( cjson zkey|0 cjson - ) 
    add-item ;

: add-array ( cjson zkey|0 cjson - ) 
    add-item ;

: add-string ( zvalue zkey|0 cjson - ) 
    -rot swap _cJSON_AddStringToObject@12 drop ;

: add-float ( f:n zkey|0 cjson - ) 
    swap _cJSON_AddNumberToObject@16 drop ;

: add-fixed ( p zkey|0 cjson - )
    2>r p>f 2r> add-float ;

: add-int ( n zkey cjson - )
    2>r s>f 2r> add-float ;

: get-string-content ( cjson - a len )
    _cJSON_GetStringValue@4 zcount ;

: get-item ( n|zkey cjson - cjson )
    {: key cjson :} 
    cjson is-array? if 
        cjson key _cJSON_GetArrayItem@8
    else
        cjson key _cJSON_GetObjectItem@8
    then 
    ?dup 0= if key f" JSON key '%z' not found!" -1 -rot .abort then ;

: get-array ( zkey cjson - cjson )
    get-item ;

: get-object ( zkey cjson - cjson )
    get-item ;

: get-string ( zkey cjson - a len ) 
    get-item _cJSON_GetStringValue@4 zcount ;

: get-float ( zkey cjson - f:n ) 
    get-item _cJSON_GetNumberValue@4 ;
    
: get-fixed ( zkey cjson - p ) 
    get-float f>p ;

: get-int ( zkey cjson - n ) 
    get-item 5 cells + @ ;
    \ get-float f>s ;

: count-array ( zkey cjson - cjson length )
    get-array dup _cJSON_GetArraySize@4 ;

public

: cjson. ( cjson - ) 
    serialize-cjson udup type _cJSON_free@4 ;

aka j. cjson.

|| 0 value path 0 value len 0 value xt 0 value cjson
continuation: serjson> ( path len [xt] - ) ( cjson - ) 
    \ {: path len xt | cjson :}
    to xt to len to path
    new-object to cjson
    cjson xt ?execute 
    cjson serialize-cjson 
        udup path len write 
        cjson-free
    cjson cjson-delete ;

\ improved accessor syntax
: @int swap get-int ;
: @float swap get-float ;
: @fixed swap get-fixed ;
: ?@fixed ['] @fixed catch if 2drop 0 then ;
: @string swap get-string ;

: @zstring 
    swap get-item _cJSON_GetStringValue@4 ;

: j@ ( json key - json' ) 
    swap get-item ;

: ?j@ ( json key - json'|0 ) 
    ['] j@ catch if 2drop 0 then ;

: jlen ( jarray - n )
    _cJSON_GetArraySize@4 ;

: jcount ( jarray - array-json n ) 
    dup _cJSON_GetArraySize@4 ;

borrow array2>> enough-flag
|| : _jeach ( ... jsrc xt - ... ) ( ... jsrc key' - ... )
    swap jcount for 
        2dup 2>r 2 rpick rot execute 
        enough-flag @ if 2r> 2drop unloop exit then
        2r>
    loop 2drop ;

: jeach ( ... jsrc xt - ... ) ( ... jsrc key' - ... )
    2dup and 0= if 2drop exit then
    enough-flag @ >r
    enough-flag off
    ['] _jeach catch
    r> enough-flag !
    throw ;

|| : >child  2 cells + @ ;
|| : >next  @ ;

|| : _jeach-fast ( ... jsrc xt - ... ) ( ... jitem - ... )
    swap jcount swap >child swap ( xt child count )
    for 
        2dup 2>r  swap execute 
        enough-flag @ if 2r> 2drop unloop exit then
        2r> >next
    loop 2drop ;

: jeach-fast ( ... jsrc xt - ... ) ( ... jitem - ... )
    2dup and 0= if 2drop exit then    
    enough-flag @ >r
    enough-flag off
    ['] _jeach-fast catch
    r> enough-flag !
    throw ;

aka j@n @int
aka j@f @float
aka j@p @fixed
aka ?j@p ?@fixed
aka j@s @string
aka j@z @zstring

: parse-json ( a len - cjson ) _cJSON_ParseWithLength@8 ;

: j>int  5 cells + @ ;