
\ =============================================================================
\ Window position save/restore
\ =============================================================================

variable winx
variable winy

: save-window-pos  
    debug @ -exit
    display winx winy al_get_window_position
    s" session.ini" [SaveConfig
        .sintvar winx
        .sintvar winy
    SaveConfig] ;

: restore-window-pos
    s" session.ini" CfgIncluded 
    display winx @ winy @ al_set_window_position ;

['] save-window-pos AtExit

\ =============================================================================
\ Window management
\ =============================================================================

extern void * GetForegroundWindow( );
extern bool SetForegroundWindow( void * hwnd );
GetForegroundWindow value vfx-hwnd
\ vfx-hwnd 0 0 0 0 0 $41 SetWindowPos drop 

: >display ( - )
    flushOP-gen drop \ flush buffered output (VFX)
    reset-keyboard 
    display al_get_win_window_handle SetForegroundWindow drop ;

: >vfx ( - )
    vfx-hwnd SetForegroundWindow drop ;

: fullscreen ( - )
    ." FULLSCREEN" cr -1 to fullscreen? ;

: windowed ( - )
    ." WINDOWED" cr 0 to fullscreen? ;

: *display ( - )
    create-display  restore-window-pos ;

\ =============================================================================
\ Color and position
\ =============================================================================

\ Color

ec-float penr
ec-float peng
ec-float penb
ec-float pena
ec-cell penx
ec-cell peny

: frgba@ ( - f:r f:g f:b f:a )
    penr@ peng@ penb@ pena@ ; 

: frgba ( f:r f:g f:b f:a - )
    pena! penb! peng! penr! ;

1e 1e 1e 1e frgba

: rgba ( n - )  
    c>f 8 << c>f 8 << c>f 8 << c>f drop frgba ;

\ Position
: at ( x y - ) peny! penx! ;
: at@ ( - x y ) penx@ peny@ ;
: at@f ( - f:x f:y ) at@ 2s>f ;
: +at ( x y - ) at@ 2+ at ;

\ =============================================================================
\ Drawing primitives
\ =============================================================================

: albmpw ( albmp - w ) dup if al_get_bitmap_width then ;
: albmph ( albmp - h ) dup if al_get_bitmap_height then ;

: draw-bitmap ( albmp n - )
    over if at@f al_draw_bitmap else 2drop then ;

: *subbmp ( albmp w h - albmp' )
    at@ 2swap al_create_sub_bitmap ?dup 
    0= abort" al_create_sub_bitmap error" ;

: -albmp ( albmp - )
    ?dup -exit al_destroy_bitmap ;

redef : cls ( - )
    0e 0e 0e 1e al_clear_to_color ;

\ =============================================================================
\ Matrix stack
\ =============================================================================

|| create mstk 16 cells 16 * allot
|| create m2 16 cells allot
|| variable mp
|| variable tx variable ty
|| : m mp @ 15 and 16 cells * mstk + ; 

: m{ ( - )
    m 1 mp +! m swap al_copy_transform ;

: m} ( - )
    -1 mp +!  m al_use_transform ;

: transform ( f:x f:y f:sx f:sy a - )
    m2 al_build_transform  m2 m al_compose_transform  m m2 al_copy_transform
    m al_use_transform ;

: identity ( - )
    m al_identity_transform  m al_use_transform ;

: 2p*m ( x. y. - x. y. )
    2p>f ty sf! tx sf!  m tx ty al_transform_coordinates
    tx sf@ f>p ty sf@ f>p ;

: inverse ( - )
    m al_invert_transform ;

\ =============================================================================
\ Text
\ =============================================================================

\ Variables

\ || variable rmargin
variable line-spacing  8 line-spacing !
ec-cell fnt

\ Text rendering

: mltext ( a len w - )
    {: | zstr[ 4096 ] lh w z :}
    over 0= if 3drop exit then 
    >r zstr[ zplace  zstr[ r> s>f f>ds   
    line-spacing @ s>f f>ds to lh to w to z 
    fnt@  frgba@  at@f w lh 0 z al_draw_multiline_text ;

: print ( a len - )
    winw penx@ - mltext ;

: print2 ( a len line-spacing - )
    line-spacing @ >r line-spacing !
    print
    r> line-spacing ! ;

: textw ( a n - n )
    {: | zstr[ 4096 ] :}
    zstr[ zplace  zstr[  fnt@  swap  al_get_text_width ;  

: glyph ( n - )
    >r  fnt@ frgba@ at@f  r> al_draw_glyph ;
