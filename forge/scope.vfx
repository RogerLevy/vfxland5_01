\ ------------------------------------------------------------------------------
\ Scope
\  Per-file public/private words support
\  Also provides search order stack and stack imbalance checking after include
\ ------------------------------------------------------------------------------

\ Public/private
create wl$ 32 allot
variable public-xt  ' forth public-xt !

: wlname
    CurrSourceName -path -ext wl$ place
    wl$ s" >>" $+ count ;

: the-wordlist  wl$ count search-context if >body @ else 0 then ;

: scope ( - )
    only forth definitions
    s" vocabulary " >pad wl$ count $+ count evaluate
    does> only forth definitions also @ context ! ;

: ?wordlist ( a n - wl )
    wl$ place
    the-wordlist ?dup if exit then
    scope 

    \ backward compatibility: also create `SCOPE/`
    s" synonym " >pad 
    wl$ count 2 - $+
    s" /" $+
    s"  " $+
    wl$ count $+
    count 
    evaluate

    the-wordlist ;

: sprivate ( a n - )
    only forth 
    ?wordlist +order definitions ;

: private ( - )
    wlname sprivate ;

: ?+order ( wl - )
    dup ?order if drop else +order then ;

: (public) ( - )
    public-xt @ >body @ set-current
    wlname ?wordlist ?+order ;

: public ( - )
    only forth (public) ;

: || ( - ) 
    \ push-order 
    private 
    0 parse evaluate 
    public ; 
    \ pop-order 
    \ the-wordlist ?+order ; 
immediate

\ Importing words from other files
: borrow ( - <vocabulary> <word> )
    push-order 
    private
    ' >body @ +order
    bl parse 2dup 
    s" synonym " >pad -rot $+ s"  " $+ -rot $+ count evaluate
    pop-order ;

\ Extend include words to preserve the search order
|| : /order only forth definitions ;

: floaded included ;
: fload getPathSpec included ;
redef : include  push-order /order getPathSpec included pop-order ;
redef : included push-order /order included pop-order ;
redef : require  push-order /order getPathSpec required pop-order ;
redef : required push-order /order required pop-order ;
: ?included  2dup fileExist? if included else 2drop then ;
: ?required  2dup fileExist? if required else 2drop then ;

only forth definitions