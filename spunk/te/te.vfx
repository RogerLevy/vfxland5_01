\ ------------------------------------------------------------------------------
\ Pinky the Penguin Tile Editor
\ ------------------------------------------------------------------------------
private

\ ------------------------------------------------------------------------------

: at@f at@ 2s>f ;

: box  ( w h - )
    0.5e f>ds >r
    at@f at@ 2+ 2s>f 1e 1e 1e 1e r>
    al_draw_rectangle ;

: nr  0  pen y@ 8 +  at ;

: gemit
    dup 10 = if  nr  1 +  exit then
    glyph 
    8 0 +at  pen x@ 320 >= if  nr  then ;

\ ------------------------------------------------------------------------------

variable t0
variable t   1 t !
\ create metas 256 cells allot&erase \ properties table
create file$ 256 allot

create syms
    char D c, \ destructible (With bomb)
    char K c, \ kill
    char C c, \ collectible
    char T c, \ trap
    24 c,
    25 c,
    27 c,
    26 c,
        
: the-tile  t0 @ t @ + ;
: the-metas  t @ cells metas + ;
: tsr>  +m transform 0 0 at r> execute -m ;
: bh  bmpwh nip ;
: row  0 swap gap * at ;
: put+  dup put 1 +  gap 0 +at ;
: (tiles)  t0 @ 16 for i row 16 for put+ loop loop drop ;
: tiles  0e 0e .5e .5e 0e tsr> (tiles) ;
: cursor-xy  t @ 16 /mod gap 2 / * swap gap 2 / * swap ;
: cursor  cursor-xy at gap 2 / dup box ;
: bigtile  0e 128e 2e 2e 0e tsr> the-tile put ;
: info  0e 128e 32e f+ .5e .5e 0e tsr> s[ t ? s] print ;
: ?0/1  swap if 15 else 8 then color syms + c@ gemit ;
: (bits)  the-metas @ 8 for dup $80 and i ?0/1 1 << loop drop ;
: bits  32e 128e 1e 1e 0e tsr> (bits) 15 color ;
: hints 32e 128e 8e f+ 1e 1e 0e tsr> s" 12345678" print ;

: ui  tiles bigtile bits hints info cursor ;

: save ( - )
    file$ count nip -exit
    metas 256 cells file$ count write
    cr ." Saved to " file$ count type ;
        
: saveas ( - <path> )
    0 parse file$ place  save ;

: load ( path c - )
    2dup file-exists? not if 2drop exit then
    t0 @ cells metas + 256 cells 2swap
    file$ place file$ count read ;

: +t  t @ + 255 and t ! ;
: flip  $80 swap for 1 >> loop the-metas @ xor the-metas ! ;
: logic
    begin event? while
        ALLEGRO_EVENT_KEY_CHAR happened? if 
            the-key <left> = if -1 +t then
            the-key <right> = if 1 +t then        
            the-key <up> = if -16 +t then
            the-key <down> = if 16 +t then
            
            '8' 1 + '1' do
                the-char i = if
                    i '1' - flip
                then
            loop
        then
        ALLEGRO_EVENT_KEY_DOWN happened? if
            the-key <s> = ctrl? and if save then
        then
    repeat ;


: edit ( - <tileset> )
    >in @ ' execute 0 tile t0 ! >in !
    bl parse -ext s" gfx\" >datadir f" %s%s.dat" load ;

public

: te  te/ show> logic ui ;
